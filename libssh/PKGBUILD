# Maintainer:   totte <totte@chakralinux.org>
# Contributors: https://git.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/libssh

pkgname=libssh
pkgver=0.9.3
pkgrel=1
pkgdesc="Library for accessing ssh client services through C libraries"
url="https://www.libssh.org/"
license=('LGPL')
arch=('x86_64')
depends=('zlib' 'openssl')
makedepends=('cmake' 'cmocka' 'doxygen' 'python3')
source=(https://libssh.org/files/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('2c8b5f894dced58b3d629f16f3afa6562c20b4bdc894639163cf657833688f0c'
            'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

prepare() {
  # disable the test. It is confused by our clean container setup.
  # 'extra-x86-build' uses user 'nobody' that has a record in /etc/passwd file
  # but $HOME envvar is set to '/build'. The test expects that $HOME corresponds to passwd file.
  sed 's/cmocka_unit_test(torture_path_expand_tilde_unix),//' -i libssh-${pkgver}/tests/unittests/torture_misc.c

  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_GSSAPI=OFF \
    -DUNIT_TESTING=ON
  make
}

check() {
  cd build
  make test
}

package(){
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
