pkgname=python2
pkgver=2.7.17
pkgrel=1
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('x86_64')
license=('PSF')
url="https://www.python.org"
depends=('bzip2' 'expat' 'gdbm' 'libffi' 'libnsl' 'openssl' 'sqlite' 'zlib')
makedepends=('tk' 'bluez-libs')
optdepends=('tk: for IDLE'
            'python2-setuptools'
            'python2-pip')
provides=('python' 'python-elementtree' 'python-ctypes')
replaces=('python-elementtree' 'python-ctypes' 'python')
conflicts=('python<3')
source=(https://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz
        'add-chakra-support.patch')
sha256sums=('4d43f033cdbd0aa7b7023c81b0e986fd11e653b5248dac9144d508f11812ba41'
            'dec83a8ca0e8505cf899c61d8c62ee44ccdd041d22799e17c99d590466ff523e')

prepare() {
  cd "${srcdir}/Python-${pkgver}"

  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite3 module to load extensions 
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py

  sed -i "s/python2.3/python2/g" Lib/distutils/tests/test_build_scripts.py \
    Lib/distutils/tests/test_install_scripts.py

  # Make sure platform.linux_distribution() returns the correct info,
  # reading it from /etc/chakra-release
  patch -p1 -i "${srcdir}/add-chakra-support.patch"

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  # clean up #!s
  find . -name '*.py' | \
    xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

  # Workaround asdl_c.py/makeopcodetargets.py errors after we touched the shebangs
  touch Include/Python-ast.h Python/Python-ast.c Python/opcode_targets.h
}

build() {
  cd "${srcdir}/Python-${pkgver}"

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --enable-optimizations \
              --with-lto \
              --enable-ipv6 \
              --enable-unicode=ucs4 \
              --with-system-expat \
              --with-system-ffi \
              --with-dbmliborder=gdbm:ndbm \
              --without-ensurepip
  
  make 
}

package() {
  cd "${srcdir}/Python-${pkgver}"

  # Hack to avoid building again
  sed -i 's/^all:.*$/all: build_all/' Makefile

  make DESTDIR="${pkgdir}" altinstall maninstall

  ln -sf "python${_pybasever}"        "${pkgdir}/usr/bin/python"
  ln -sf "python${_pybasever}"        "${pkgdir}/usr/bin/python2"
  ln -sf "python${_pybasever}-config" "${pkgdir}/usr/bin/python2-config"
  ln -sf "python${_pybasever}.1"      "${pkgdir}/usr/share/man/man1/python2.1"

  ln -sf python-${_pybasever}.pc      "${pkgdir}"/usr/lib/pkgconfig/python2.pc

  ln -sf "../../libpython${_pybasever}.so" \
    "${pkgdir}/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so"

  mv "${pkgdir}/usr/bin/smtpd.py" "${pkgdir}/usr/lib/python${_pybasever}"

  # some useful "stuff"
  install -dm755 "${pkgdir}/usr/lib/python${_pybasever}/Tools"/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/i18n"
  install -m755 Tools/scripts/{README,*py} \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts"

  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" \
    "${pkgdir}/usr/lib/python${_pybasever}/config/Makefile"

  # license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}  
