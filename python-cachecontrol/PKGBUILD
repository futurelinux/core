# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=cachecontrol
pkgbase=python3-cachecontrol
pkgname=('python3-cachecontrol' 'python2-cachecontrol')
pkgver=0.12.6
pkgrel=1
pkgdesc="httplib2 caching for requests"
arch=('any')
url="https://github.com/ionrock/${_pkgname}"
license=('Apache')
makedepends=('python3-msgpack' 'python3-requests' 'python2-msgpack' 'python2-requests' 'python3-setuptools' 'python2-setuptools')
checkdepends=('python3-mock' 'python3-pytest' 'python3-lockfile' 'python3-cherrypy'
              'python2-mock' 'python2-pytest' 'python2-lockfile' 'python2-cherrypy')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "0001-Remove-unnecessary-console-script.patch")
sha256sums=('2ec120838f445bff473cd8b8206ddae3650679142976927179743fabb876f3e3'
            'a2c93d4852887152027140bdd54030d5363876b02e5eabee6a018d4e946a87b1')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    patch -p1 -i ../0001-Remove-unnecessary-console-script.patch
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python3 setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python3 -m pytest
    python2 -m pytest
}

package_python3-cachecontrol() {
    depends=('python3-msgpack' 'python3-requests')
    optdepends=('python3-lockfile: for the FileCache')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-cachecontrol() {
    depends=('python2-msgpack' 'python2-requests')
    optdepends=('python2-lockfile: for the FileCache')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
