pkgname=sdl2_image
pkgver=2.0.5
pkgrel=1
pkgdesc="SDL2 image libraries"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('sdl2' 'libpng' 'libtiff' 'libjpeg' 'libwebp')
makedepends=('cmake')
source=("http://www.libsdl.org/projects/SDL_image/release/SDL2_image-${pkgver}.tar.gz"
        "FindSDL2_image.cmake")
sha256sums=('bdd5f6e026682f7d7e1be0b6051b209da2f402a2dd8bd1c4bd9c25ad263108d0'
            '177afccde2a7e6c112f8d2fa75d7ad72e0e4827f307f1e4dac67f7756c0b8b19')

build() {
  cd SDL2_image-$pkgver
  ./configure --disable-static --prefix=/usr
  make
}

package() {
  cd SDL2_image-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm644 COPYING.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
  
  # we can remove the following when provided directly by cmake
  install -Dm644 $srcdir/FindSDL2_image.cmake ${pkgdir}/usr/share/cmake-3.0/Modules/FindSDL2_image.cmake
}

