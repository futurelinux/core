# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kwidgetsaddons
pkgver=5.64.0
pkgrel=2
pkgdesc='Higher level user interface elements for common tasks'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kwidgetsaddons'
license=('LGPL')
depends=("qt5-base")
makedepends=("extra-cmake-modules>=5.64.0" 'qt5-tools' 'clang' 'python3-pyqt5' 'doxygen' 'python3-sip-pyqt5')
optdepends=('python3-pyqt5: for the Python bindings')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('a2d4a47489621d095c4979ea25d5d8304cf4004b10a892a2b314d74cd30cb5da'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_QCH=ON ../${pkgname}-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
