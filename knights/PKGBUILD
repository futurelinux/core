# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=knights
pkgver=19.12.3
pkgrel=1
pkgdesc="Chess board by KDE with XBoard protocol support"
arch=(x86_64)
url="https://www.kde.org/applications/games/knights/"
license=(GPL LGPL FDL)
depends=(libkdegames kplotting plasma-framework hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('gnuchess: for playing against the computer')
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4796654dcaff355b4f1097260748cfe04812ff926acc7ca0f037711875dd1512'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
