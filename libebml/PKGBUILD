pkgname=libebml
pkgver=1.3.9
pkgrel=1
pkgdesc="Extensible Binary Meta Language library"
arch=('x86_64')
url="https://dl.matroska.org/downloads/libebml/"
license=('LGPL')
depends=('gcc-libs')
makedepends=('cmake')
source=("https://dl.matroska.org/downloads/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('319f99898751052f13334c0df7d19d02')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}
