# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=qt5-webengine
_qtver=5.14.1
pkgver=${_qtver/-/}
pkgrel=7
arch=('x86_64')
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel'
         'qt5-location'
         'libxcomposite'
         'libxrandr'
         'pciutils'
         'libxss'
         'libevent'
         'snappy'
         'nss'
         'libxslt'
         'minizip'
         'ffmpeg'
         're2'
         'libvpx')
makedepends=('python2'
             'gperf'
             'jsoncpp'
             'ninja'
             'qt5-tools'
             'poppler')
groups=('qt5')
options=('debug')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('4ec77040a876a83aa2a833ebfe7b3e88dcc167ceb317095eb226a0b8d455e887')

prepare() {
  mkdir -p build

  # TODO: sed -e 's|7-9|7-9\|10|' -i $_pkgfqn/configure.pri # Support ninja 1.10
}

build() {
  cd build

  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker
    #-webengine-icu
    #-webengine-kerberos
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because of reference to the build directory
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
