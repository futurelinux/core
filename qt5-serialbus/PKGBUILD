
pkgname=qt5-serialbus
pkgver=5.13.1
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Qt module for general purpose serial bus access'
depends=('qt5-serialport')
makedepends=()
groups=('qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${pkgver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/${_pkgfqn}.tar.xz"
        '0001-include-linux-sockios.h.patch')
sha256sums=('643bf5512212a1dc89047d25ed3f766bf08362c143f1da485f5235d3023ca565'
            '3f0adc92232798f5375c73a7e413a95a817bfb32f652d6f668aed039a82f4595')

prepare() {
  mkdir -p build
  cd ${_pkgfqn}/
  patch -Np1 -i $srcdir/0001-include-linux-sockios.h.patch
}

build() {
  cd build

  qmake-qt5 ../${_pkgfqn}  
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
