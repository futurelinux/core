# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=polkit-kde-agent
pkgver=5.18.4.1
pkgrel=1
pkgdesc='Daemon providing a polkit authentication UI for KDE'
arch=('x86_64')
url='https://projects.kde.org/projects/extragear/base/polkit-kde-agent-1/'
license=('LGPL')
depends=(kiconthemes kdbusaddons kcrash)
makedepends=(extra-cmake-modules kdoctools python)
source=("https://download.kde.org/stable/plasma/${pkgver%.*}/$pkgname-1-$pkgver.tar.xz"{,.sig})
sha256sums=('b1e20048d1f917dfeabb150d1147a6add02e7cfb333df8d73f9fabf0a643ff48'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-1-$pkgver \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"  -DLIBEXEC_INSTALL_DIR=lib/polkit-kde
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
