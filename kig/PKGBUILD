# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kig
pkgver=19.12.3
pkgrel=1
pkgdesc="Interactive geometry"
url="https://kde.org/applications/education/kig/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(ktexteditor qt5-xmlpatterns boost-libs hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         pykig-python3.patch)
sha256sums=('1ae2c3024cdd14e476ff15b730f4ebe9b279477b67cc4cc89606755c7d3beef3'
            'SKIP'
            'b801af24d560dd3928657cbcaed879b794b13038e0785681f5ba82368d1a8fd4')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../pykig-python3.patch # Port pykig to python 3
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
