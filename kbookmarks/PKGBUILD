# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kbookmarks
pkgver=5.68.0
pkgrel=2
pkgdesc='Support for bookmarks and the XBEL format'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kbookmarks'
license=('LGPL')
depends=('kxmlgui')
makedepends=("extra-cmake-modules" 'qt5-tools' 'python3')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('80dc06188a5e1d960d46f527bd82d9b79df75a785164fa29a088a7b705abbf84'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
