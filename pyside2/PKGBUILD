# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=pyside2
_qtver=5.14.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url="https://wiki.qt.io/Qt_for_Python"
license=(LGPL)
pkgdesc='Enables the use of Qt5 APIs in Python applications'
depends=('python-shiboken2' 'qt5-base')
makedepends=('shiboken2' 'cmake' 'llvm' 'clang'
            'qt5-multimedia' 'qt5-tools' 'qt5-sensors' 'qt5-charts' 'qt5-webengine' 'qt5-datavis3d'
            'qt5-websockets' 'qt5-speech' 'qt5-3d' 'qt5-svg' 'qt5-script' 'qt5-scxml' 'qt5-x11extras' 'qt5-remoteobjects')
optdepends=('qt5-svg: QtSvg bindings'
            'qt5-script: QtScript bindings'
            'qt5-speech: QtTextToSpeech bindings'
            'qt5-websockets: QtWebSockets bindings'
            'qt5-webengine: QtWebEngine bindings'
            'qt5-datavis3d: QtDataVisualization bindings'
            'qt5-scxml: QtScxml bindings'
            'qt5-sensors: QtSensors bindings'
            'qt5-3d: Qt3D bindings'
            'qt5-x11extras: QtX11Extras bindings'
            'qt5-charts: QtCharts bindings'
            'qt5-tools: QtHelp bindings'
            'qt5-remoteobjects: QtRemoteObjects bindings')
groups=(qt5)
conflicts=(python-pyside2)
provides=(python-pyside2)
replaces=(python-pyside2)
_pkgfqn=pyside-setup-opensource-src-${_qtver}
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('41ce931695567639c92acb68a9d66ed5609f067011af9a94b53fc0d697ad1d1c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/pyside2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

package() {
  cd build

  make DESTDIR=$pkgdir install
}
