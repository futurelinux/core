# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kdewebkit
pkgver=5.61.0
pkgrel=1
pkgdesc='KDE Integration for QtWebKit'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdewebkit'
license=('LGPL')
depends=(qt5-webkit kparts)
makedepends=("extra-cmake-modules>=5.61.0" qt5-tools)
options=("debug")
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('1ee2a00ee3d95df9270e8c3d434568cda8f42151e361bc07fe374bf0f7afe211'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check(){
#   cd build
#   make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
