# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: Gaute Hope <eg@gaute.vetsj.com>
# Contributor: Melissa Padilla <mpadilla2 at hotmail dot com>

pkgbase=python-future
pkgname=(python3-future python2-future)
pkgver=0.17.1
pkgrel=1
pkgdesc="Clean single-source support for Python 3 and 2"
url="https://python-future.org/"
arch=('any')
license=('MIT')
makedepends=('python3-setuptools' 'python2-setuptools')
checkdepends=('python3-requests' 'python2-requests')
options=('!emptydirs')
source=("https://pypi.io/packages/source/f/future/future-$pkgver.tar.gz")
sha512sums=('50094c44db81afbfd37ee25e1710ed11ecb2c2d64ad076fb62a2688dd36a9696ab26b0d7989bf1ecb10da251961a0ee6bb37c4dbd115f70384cd8c9a675d0b2d')

prepare() {
  cp -a future-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/future-$pkgver
  python3 setup.py build

  cd "$srcdir"/future-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/future-$pkgver
#PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python setup.py test || warning "Tests failed"

  cd "$srcdir"/future-$pkgver-py2
#PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 setup.py test
}

package_python3-future() {
  depends=('python3')
  optdepends=('python3-setuptools: futurize and pasteurize scripts')
  provides=('futurize' 'pasteurize')

  cd future-$pkgver

  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-future() {
  depends=('python2')
  optdepends=('python2-setuptools: futurize2 and pasteurize2 scripts')

  cd future-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/futurize{,2}
  mv "$pkgdir"/usr/bin/pasteurize{,2}

  install -D -m644 LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
