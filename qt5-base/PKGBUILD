# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgbase=qt5-base
pkgname=(qt5-base qt5-xcb-private-headers)
_qtver=5.14.1
pkgver=${_qtver/-/}
pkgrel=2
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework'
depends=('libjpeg-turbo'
         'xcb-util-keysyms'
         'xcb-util-renderutil'
         'libgl'
         'fontconfig'
         'xdg-utils'
         'shared-mime-info'
         'xcb-util-wm'
         'libxrender'
         'libxi'
         'sqlite'
         'xcb-util-image'
         'mesa'
         'tslib'
         'libinput'
         'libxkbcommon-x11'
         'libproxy'
         'libcups'
         'double-conversion'
         'icu' # TODO: Is this necessary?
         'pcre2' # TODO: Is this necessary?
         'libsm') # TODO: Is this necessary?
         # TODO: Add md4c? https://www.archlinux.org/packages/extra/x86_64/md4c/
makedepends=('mtdev' # TODO: Is this necessary?
             'libmariadbclient' # Note: Named 'mariadb-libs' in Arch Linux
             'sqlite'
             'unixodbc'
             'postgresql-libs'
             'alsa-lib'
             'gst-plugins-base-libs'
             'libpulse'
             'cups'
             'freetds'
             'vulkan-headers')
optdepends=('qt5-svg: to use SVG icon themes'
            'qt5-translations: for some native UI translations'
            'qt5-wayland: to run Qt applications in a Wayland session'
            'postgresql-libs: PostgreSQL driver'
            'libmariadbclient: MariaDB driver'
            'unixodbc: ODBC driver'
            'freetds: MS SQL driver'
            'gtk3: GTK platform plugin'
            'mtdev: evdev plugin') # TODO: Is this necessary?
groups=('qt5')
options=('debug')
_pkgfqn="${pkgbase/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        qt5-base-cflags.patch)
sha256sums=('d9d423a6e7bcf1055c0372fc029f14a6fe67dd62c67b83095cde68b60b762cf7'
            'c70640499637314df68cc7a56519f0bf8b65dd808dd982c96c6f045bba993adf')

prepare() {
  cd ${_pkgfqn}

  # Build qmake using Chakra {C,LD}FLAGS
  # This also sets default {C,CXX,LD}FLAGS for projects built using qmake
  sed -i -e "s|^\(QMAKE_CFLAGS_RELEASE.*\)|\1 ${CFLAGS}|" \
    mkspecs/common/gcc-base.conf
  sed -i -e "s|^\(QMAKE_LFLAGS_RELEASE.*\)|\1 ${LDFLAGS}|" \
    mkspecs/common/g++-unix.conf

  # Use Python 3
  find . -name '*.py' -exec sed -i \
    's|#![ ]*/usr/bin/python$|&2|;s|#![ ]*/usr/bin/env python$|&3|' {} +
}

build() {
  cd ${_pkgfqn}

  PYTHON=/usr/bin/python3 ./configure -confirm-license -opensource -v \
    -prefix /usr \
    -bindir /usr/lib/qt5/bin \
    -docdir /usr/share/doc/qt5 \
    -headerdir /usr/include/qt5 \
    -archdatadir /usr/lib/qt5 \
    -datadir /usr/share/qt5 \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt5/examples \
    -plugindir /usr/lib/qt5/plugins \
    -importdir /usr/lib/qt5/imports \
    -qmldir /usr/lib/qt5/qml \
    -testsdir /usr/share/qt5/tests \
    -translationdir /usr/share/qt5/translations \
    -plugin-sql-{psql,mysql,sqlite,odbc} \
    -system-sqlite \
    -openssl-linked \
    -nomake tests \
    -nomake examples \
    -no-rpath \
    -optimized-qmake \
    -dbus-linked \
    -system-harfbuzz \
    -journald \
    -reduce-relocations
  make
}

package_qt5-base() {
  pkgdesc='A cross-platform application and UI framework'

  cd ${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because of reference to the build directory
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in .pri file
  sed -i "s|${srcdir}/${_pkgfqn}|/usr|" \
    "${pkgdir}"/usr/lib/qt5/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Useful symlinks
  install -d "${pkgdir}"/usr/bin
  for b in "${pkgdir}"/usr/lib/qt5/bin/*; do
    ln -s /usr/lib/qt5/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done
}

package_qt5-xcb-private-headers() {
  pkgdesc='Private headers for Qt5 Xcb'
  depends=("qt5-base=$pkgver")
  optdepends=()
  groups=()
  conflicts=()

  cd ${_pkgfqn}
  install -d -m755 "$pkgdir"/usr/include/qtxcb-private
  cp -r src/plugins/platforms/xcb/*.h "$pkgdir"/usr/include/qtxcb-private/
}
