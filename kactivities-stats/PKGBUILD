# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kactivities-stats
pkgver=5.68.0
pkgrel=1
pkgdesc="A library for accessing the usage data collected by the activities system"
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kactivities-stats'
license=('LGPL')
depends=('kactivities')
makedepends=("extra-cmake-modules>=5.64.0" 'boost' 'python3')
options=("debug")
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('fb645db4685113dfd98834f48d8941529fee53d5e26ec5e36cfee8a9bfae97ae'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 
  make
}

package() {
  cd build
  
  make  DESTDIR="${pkgdir}"  install
}

