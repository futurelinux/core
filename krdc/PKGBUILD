# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname="krdc"
arch=('x86_64')
pkgver=19.12.3
pkgrel=1
pkgdesc="Remote Desktop Client"
url="http://kde.org/applications/internet/krdc/"
license=('GPL' 'LGPL' 'FDL')
depends=('kcmutils' 'kdnssd' 'knotifyconfig')
makedepends=(extra-cmake-modules kdoctools libvncserver freerdp)
optdepends=('libvncserver: VNC support'
            'freerdp: RDP support'
            'keditbookmarks: to edit bookmarks')
options=('docs' 'debug')
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('12602912abbc22e061067b6b5048d37a7cbdaaf99d203829d3f52fdf7319acce'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
