pkgname=m17n-lib
pkgver=1.8.0
pkgrel=1
pkgdesc='Multilingual text processing library (runtimes)'
url='http://www.nongnu.org/m17n/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libxft' 'm17n-db' 'fribidi' 'libxml2' 'gd' 'libotf')
optdepends=('anthy: to use the anthy input method')
options=('!makeflags' '!emptydirs')
source=("https://download.savannah.gnu.org/releases/m17n/${pkgname}-${pkgver}.tar.gz")
sha1sums=('298923e2f7f6b8763769b74c8b2da8fe4d13ecce')

prepare() {
	cd ${pkgname}-${pkgver}
	sed -i 's/freetype-config/pkg-config freetype2/' configure
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}
 
package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
