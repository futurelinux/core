pkgname=cifs-utils
pkgver=6.7
pkgrel=1
pkgdesc="CIFS filesystem user-space tools"
arch=('x86_64')
url="http://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap-ng' 'keyutils' 'krb5' 'talloc' 'libwbclient' 'pam')
source=("https://download.samba.org/pub/linux-cifs/cifs-utils/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('b2f21612474ab012e75accd167aab607a0614ff67efb56ea0f36789fa785cfab')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # systemd support is broken in mount.cifs
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --disable-systemd
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" ROOTSBINDIR=/usr/bin install
  mkdir -p $pkgdir/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.idmap.conf $pkgdir/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.spnego.conf $pkgdir/etc/request-key.d
  # set mount.cifs uid, to enable none root mounting form fstab
  chmod +s $pkgdir/usr/bin/mount.cifs
  # fix idmap-plugin #42052
  mkdir -p $pkgdir/etc/cifs-utils
  ln -s /usr/lib/cifs-utils/idmapwb.so ${pkgdir}/etc/cifs-utils/idmap-plugin
}
