# maintainer almack@chakraos.org

pkgname=glew
pkgver=2.1.0
pkgrel=1
pkgdesc="A cross-platform C/C++ extension loading library"
arch=('x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'glu')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tgz")
sha1sums=('18eca05460d0b61709fc115e632b6fe320718b44')

build() {
  cd $pkgname-$pkgver
  sed -i 's|lib64|lib|' config/Makefile.linux
  sed -i '/^.PHONY: .*\.pc$/d' Makefile
  make
}

package() {
  cd $pkgname-$pkgver
  make GLEW_DEST="$pkgdir/usr" install.all
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
