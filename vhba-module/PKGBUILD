pkgbase=vhba-module
pkgname=(vhba-module vhba-module-dkms)
pkgver=20190410
pkgrel=1
pkgdesc="Kernel module that emulates SCSI devices"
arch=('x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=("linux=5.2.9")
makedepends=("linux-headers=5.2.9")
_extramodules=extramodules-CHAKRA
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        60-vhba.rules  dkms.conf)
md5sums=('dc2323bd121a0f5e389f00d7e0aa97aa'
         '4dc37dc348b5a2c83585829bde790dcc'
         '7da9314812d9ea665df0db33052c7452')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/20190302/20190410/' Makefile  # Fixup VHBA_VERSION
}

build() {
  cd $pkgbase-$pkgver
  make KERNELRELEASE="$(cat /usr/lib/modules/$_extramodules/version)"
}

package_vhba-module() {
  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 *.ko
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"

  find "$pkgdir" -name '*.ko' -exec xz {} +
}

package_vhba-module-dkms() {
  depends=(dkms)
  provides=("vhba-module=$pkgver-$pkgrel")
  conflicts=(vhba-module)

  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/src/$pkgbase-$pkgver" -m644 Makefile vhba.c ../dkms.conf
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"
}
