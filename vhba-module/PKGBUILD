pkgbase=vhba-module
pkgname=(vhba-module vhba-module-dkms)
pkgver=20190831
pkgrel=1
pkgdesc="Kernel module that emulates SCSI devices"
arch=('x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
makedepends=("linux-headers=5.3.11")
source=("https://downloads.sourceforge.net/cdemu/$pkgbase-$pkgver.tar.bz2"
              60-vhba.rules  dkms.conf)
md5sums=('e78ce00b01fd9691e2328818f9bb8a16'
         '4dc37dc348b5a2c83585829bde790dcc'
         '7da9314812d9ea665df0db33052c7452')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/20190302/20190410/' Makefile  # Fixup VHBA_VERSION
}

build() {
  cd $pkgbase-$pkgver
  make KERNELRELEASE="$(</usr/src/linux/version)"
}

package_vhba-module() {
  depends=(linux)

  local extradir="/usr/lib/modules/$(</usr/src/linux/version)/extramodules"
  cd $pkgbase-$pkgver
  install -Dt "$pkgdir$extradir" -m644 *.ko
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"

  find "$pkgdir" -name '*.ko' -exec xz {} +
}

package_vhba-module-dkms() {
  depends=(dkms)
  provides=("vhba-module=$pkgver-$pkgrel")
  conflicts=(vhba-module)

  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/src/$pkgbase-$pkgver" -m644 Makefile vhba.c ../dkms.conf
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"
}
