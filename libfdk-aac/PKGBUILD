pkgname=libfdk-aac
pkgver=2.0.1
pkgrel=1
pkgdesc='Fraunhofer FDK AAC codec library'
arch=('x86_64')
url='https://sourceforge.net/projects/opencore-amr/'
license=('custom')
provides=(libfdk-aac.so)
source=("https://downloads.sourceforge.net/opencore-amr/fdk-aac-${pkgver}.tar.gz")
sha256sums=('840133aa9412153894af03b27b03dde1188772442c316a4ce2a24ed70093f271')

build() {
  cd fdk-aac-${pkgver}

  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

package () {
  cd fdk-aac-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm644 NOTICE "${pkgdir}"/usr/share/licenses/libfdk-aac/NOTICE
}
