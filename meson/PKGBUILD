pkgname=meson
pkgver=0.53.1
pkgrel=1
pkgdesc='High productivity build system'
url='https://mesonbuild.com/'
arch=('x86_64')
license=('Apache')
depends=('python3-setuptools' 'ninja')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc}
        chakra-meson
        skip-test.diff
        0001-Fix-CUDA-test-without-devices.patch)
sha256sums=('ec1ba33eea701baca2c1607dac458152dc8323364a51fdef6babda2623413b04'
            'SKIP'
            '0998d9f6c35494c55e70695e5ae6eca275f0536ca3e730e1f052e9092a47d841'
            'a4968c7b5372148fcc6ce9c5196ccb266844043e25d76917af8cf93767738bcc'
            '7e241acdc574b193b931648f487feceee79af4b898e5481eb4dbbc2d2fc89490')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}

  # Our containers do not allow sanitizers to run
  patch -Np1 -i ../skip-test.diff

  # Fix CUDA test without devices
  patch -Np1 -i ../0001-Fix-CUDA-test-without-devices.patch
}

build() {
  cd ${pkgname}-${pkgver}
  python3 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -rt "${pkgdir}/usr/share/vim/vimfiles" data/syntax-highlighting/vim/*/

  install -Dt "${pkgdir}/usr/share/emacs/site-lisp" -m644 data/syntax-highlighting/emacs/*
  install -Dt "${pkgdir}/usr/share/zsh/site-functions" -m644 data/shell-completions/zsh/*

  # Chakra packaging helper
  install -D ../chakra-meson -t "${pkgdir}/usr/bin"
}
