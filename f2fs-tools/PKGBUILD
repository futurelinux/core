pkgname=f2fs-tools
pkgver=1.13.0
pkgrel=1
pkgdesc="Tools for Flash-Friendly File System (F2FS)"
arch=('x86_64')
url='http://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/about/'
depends=('util-linux')
license=('GPL')
source=("https://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/snapshot/$pkgname-$pkgver.tar.gz")
sha256sums=('b39d3ae9224267aab0070b5f17d91d0c5143f6d960166a27f6b11c8c87072c7d')

build() {
	cd "${pkgname}-${pkgver}"

	# sg_write_buffer is provided in sg3_utils
	sed -i '/sg_write_buffer/d' tools/Makefile.am

	autoreconf -fi
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" sbindir=/usr/bin install
}
