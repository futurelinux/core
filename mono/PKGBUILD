pkgname=mono
pkgver=5.18.1.0
_pkgver=5.18.1
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=('x86_64')
license=('GPL' 'LGPL2.1' 'MPL')
url="https://www.mono-project.com/"
depends=('zlib' 'libgdiplus>=4.2' 'sh' 'python3' 'ca-certificates')
makedepends=('cmake')
provides=('monodoc')
conflicts=('monodoc')
install=mono.install
source=(https://download.mono-project.com/sources/mono/$pkgname-$pkgver.tar.bz2
        mono.binfmt.d)
sha256sums=('a92a8fb54f8faa922cdb203cf0bee93bf418f08144f5bc018c5013948d03a4f0'
            '7aa517de1307247a133d1b55051243fb9ec49c3cadb0de75c1be9687183b3ae2')

build() {
  cd $pkgname-$_pkgver

  # build mono
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --with-mcs-docs=no
  make
  make -C mcs/jay
}

package() {
  cd $pkgname-$_pkgver
  make DESTDIR=$pkgdir install
  make -C mcs/jay DESTDIR="${pkgdir}" prefix=/usr INSTALL=../../install-sh install
  install -Dm 644 "${srcdir}/mono.binfmt.d" "${pkgdir}/usr/lib/binfmt.d/mono.conf"
}
