# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/fluidsynth

pkgname=fluidsynth
pkgver=2.1.2
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'libpulse' 'portaudio' 'sdl2')
makedepends=('cmake' 'ladspa' 'doxygen')
optdepends=('pulseaudio: PulseAudio sound support')
license=('LGPL')
backup=("etc/conf.d/$pkgname")
source=($pkgname-$pkgver.tar.gz::"https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('ece3328ee94731f347a03f555fd39ead0a50cb24fef5d90dbff22e74db8344c9a68bfaccb76b873bd18a8ac68ee1d7802571e51c938bbf98e424267376f47eb8')


prepare() {
  cd "${pkgname}-${pkgver}"
  # out-of-tree build
  mkdir build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
           -Denable-ladspa=ON \
           -Denable-lash=ON \
           -Denable-portaudio=ON \
           -DLIB_SUFFIX=""
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm644 "${pkgname}.service" \
    "$pkgdir/usr/lib/systemd/user/${pkgname}.service"
  # system-wide configuration file
  install -vDm644 "${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../{AUTHORS,ChangeLog,CONTRIBUTING.md,README.md,THANKS,TODO}
}
