# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/fluidsynth

pkgname=fluidsynth
pkgver=2.0.1
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'libpulse' 'portaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
optdepends=('pulseaudio: PulseAudio sound support')
license=('LGPL')
backup=("etc/conf.d/$pkgname")
source=($pkgname-$pkgver.tar.gz::"https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('cdb6a70ced339de74fdc118e504590d41951c268f65675987f7a80f626cd542e808a54d1a6835f5ca1f892f5c2542ad56a5c3a82714577feb2d2122126dc2f08')


prepare() {
  cd "${pkgname}-${pkgver}"
  # out-of-tree build
  mkdir build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
           -Denable-ladspa=ON \
           -Denable-lash=ON \
           -Denable-portaudio=ON \
           -DLIB_SUFFIX=""
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm644 "${pkgname}.service" \
    "$pkgdir/usr/lib/systemd/user/${pkgname}.service"
  # system-wide configuration file
  install -vDm644 "${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../{AUTHORS,ChangeLog,NEWS,README.md,THANKS,TODO}
}
