pkgbase=python-lxml
pkgname=('python3-lxml' 'python2-lxml' 'python-lxml-docs')
pkgver=4.3.3
pkgrel=1
arch=('x86_64')
license=('BSD' 'custom')
url="https://lxml.de/"
makedepends=('libxslt' 'python3-setuptools' 'python2-setuptools' 'epydoc' 'python2-pygments'
             'python2-docutils' 'cython3' 'cython2' 'python3-cssselect' 'python2-cssselect'
             'python3-html5lib' 'python2-html5lib' 'python3-beautifulsoup4' 'python2-beautifulsoup4')
source=("https://github.com/lxml/lxml/archive/lxml-$pkgver.tar.gz")
sha256sums=('975d56a3993479a6ed422a78440de6e820691222427e962eec82de25a9d04511')

prepare() {
  mv lxml-lxml-$pkgver lxml-$pkgver
  cp -a lxml-$pkgver{,-py2}

  # Setting LC_CTYPE to workaround encoding issue
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/lxml-$pkgver
  python3 setup.py build

  cd "$srcdir"/lxml-$pkgver-py2
  python2 setup.py build
  make PYTHON=python2 html
}

package_python3-lxml() {
  pkgdesc="Python3 binding for the libxml2 and libxslt libraries"
  depends=('python3' 'libxslt')
  optdepends=('python3-beautifulsoup4: support for beautifulsoup parser to parse not well formed HTML'
              'python3-cssselect: support for cssselect'
              'python3-html5lib: support for html5lib parser'
              'python-lxml-docs: offline docs')

  cd lxml-$pkgver
  python3 setup.py install --root="$pkgdir" --skip-build --optimize=1
  
  install -Dm644 LICENSES.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 doc/licenses/BSD.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/BSD.txt
  install -Dm644 doc/licenses/elementtree.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/elementtree.txt
}

package_python2-lxml() {
  pkgdesc="Python2 binding for the libxml2 and libxslt libraries"
  depends=('python2' 'libxslt')
  optdepends=('python2-beautifulsoup4: support for beautifulsoup parser to parse not well formed HTML'
              'python2-cssselect: support for cssselect'
              'python2-html5lib: support for html5lib parser'
              'python-lxml-docs: offline docs')

  cd lxml-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
 
  install -Dm644 LICENSES.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 doc/licenses/BSD.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/BSD.txt
  install -Dm644 doc/licenses/elementtree.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/elementtree.txt
}

package_python-lxml-docs() {
  pkgdesc="Python binding for the libxml2 and libxslt libraries (docs)"
  options=('docs')

  cd lxml-$pkgver
  install -d "$pkgdir"/usr/share/doc/$pkgbase
  cp -r doc/html "$pkgdir"/usr/share/doc/$pkgbase

  install -Dm644 LICENSES.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 doc/licenses/BSD.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/BSD.txt
  install -Dm644 doc/licenses/elementtree.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/elementtree.txt
}
