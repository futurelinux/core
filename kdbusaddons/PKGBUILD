# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kdbusaddons
pkgver=5.68.0
pkgrel=2
pkgdesc='Provides API for registering services, KDED modules and applications according to Freedesktop standards.'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdbusaddons'
license=('LGPL')
depends=("qt5-base" 'qt5-x11extras')
makedepends=("extra-cmake-modules>=5.68.0" 'qt5-tools')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('839fe42f9ac8df353f87245110fd7b515a8eb29f0840f54481bd89e5175bf1af'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
