# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=modemmanager-qt
pkgver=5.64.0
pkgrel=1
pkgdesc="Plasma Workspace"
arch=('x86_64')
url='http://projects.kde.org'
license=('LGPL')
depends=('qt5-base' 'networkmanager')
makedepends=("extra-cmake-modules>=5.64.0" 'kapidox')
provides=("libmm-qt5=${pkgver}")
replaces=("libmm-qt5<=5.2.2")
conflicts=("libmm-qt5<=5.2.2")
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('a9d8554b3720cf46aaaa70da87c79688afc5baa155ffd19ea00e4cae2a1caa21'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
