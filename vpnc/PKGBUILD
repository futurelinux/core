# maintainer : <inkane@chakra-project.org>
# contributor: <abveritas[at]chakra-project[dot]org>

pkgname=vpnc
_vpnc_commit=101208be5b74039ea70b9e007ce0d6c9fbe44d82
_vpncscripts_commit=c84fb8e5a523a647a01a1229a9104db934e19f00
pkgver=0.5.3.r462.r78
pkgrel=1
pkgdesc="VPN client for cisco3000 VPN Concentrators"
url="http://www.unix-ag.uni-kl.de/~massar/vpnc/"
license=('GPL')
depends=('libgcrypt' 'openssl' 'iproute2')
optdepends=('openresolv: Let vpnc manage resolv.conf')
makedepends=('git')
arch=('x86_64')
source=("vpnc::git+https://github.com/streambinder/vpnc#commit=${_vpnc_commit}"
        "vpnc-scripts::git://git.infradead.org/users/dwmw2/vpnc-scripts.git#commit=${_vpncscripts_commit}"
        vpnc.conf
        vpnc@.service
        vpnc-systemd-resolved.patch)
md5sums=('SKIP'
         'SKIP'
         'a3f4e0cc682f437e310a1c86ae198e45'
         '09cfded435c43dd2adb5a8863bd74cfc'
         '140c777825780cc502dc95961a05eeba')
backup=(etc/vpnc/default.conf)

pkgver() {
  cd ${pkgname}
  printf "%s.r%s.r%s" "$(cat VERSION)" \
    "$(git -C ../vpnc rev-list --count HEAD)" \
    "$(git -C ../vpnc-scripts rev-list --count HEAD)"
}

prepare() {
  cd vpnc-scripts
  patch -Np1 < ../vpnc-systemd-resolved.patch

  cd ../${pkgname}
  # Build hybrid support
  sed 's|^#OPENSSL|OPENSSL|g' -i Makefile
  # fix resolvconf location for community/openresolv
  sed 's|/sbin/resolvconf|/usr/bin/resolvconf|g' -i ../vpnc-scripts/vpnc-script
  ln -sf ../../vpnc-scripts/vpnc-script src
  ln -sf ../../vpnc.conf src
}

build() {
  make -C ${pkgname}
}

check() {
  make -C ${pkgname} test
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" PREFIX=/usr SBINDIR=/usr/bin install
  install -Dm 644 ../vpnc@.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
