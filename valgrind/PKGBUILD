pkgname=valgrind
pkgver=3.15.0
pkgrel=1
pkgdesc="A tool to help find memory-management problems in programs"
arch=('x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc=2.29' 'perl')
makedepends=('gdb' 'openmpi' 'lib32-glibc' 'lib32-gcc-libs')
optdepends=('openmpi: MPI support'
            'lib32-glibc: 32-bit ABI support')
options=('!emptydirs' '!buildflags')
# patches taken from Arch
source=(https://sourceware.org/pub/valgrind/valgrind-${pkgver}.tar.bz2)
sha1sums=('4cc014e2390c4bcecb11aa00b37aa52d352db97f')

prepare() {
  cd ${pkgname}-${pkgver}
  #patch -Np1 < ../valgrind-3.7.0-respect-flags.patch
}

build() {
  # valgrind does not like stack protector flags
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fstack-protector-strong -fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fstack-protector-strong -fno-plt/}

  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
