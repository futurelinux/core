# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: P Miller <philm@chakra-project.org>
#               Jan de Groot <jgc@archlinux.org>
#               Douglas Soares de Andrade <dsa@aur.archlinux.org>
#               Angel Velasquez (angvp) <angvp@archlinux.com.ve>

pkgbase=python-numpy
pkgname=('python3-numpy' 'python2-numpy')
pkgver=1.16.3
pkgrel=4
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="http://numpy.org/"
makedepends=('cblas' 'lapack' 'python3' 'python2' 'python3-setuptools' 'python2-setuptools'
             'gcc-fortran' 'python3-nose' 'python2-nose' 'cython3' 'cython2')
checkdepends=('python3-pytest' 'python2-pytest')
options=('staticlibs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/numpy/numpy/archive/v$pkgver.tar.gz")
sha512sums=('f9caa1773ec38f6e737d7732083515dc32709c8127d137e51cf35138391a66b012b84592ef473c60846fd3178ed392aa81c4e8d27adb24e5746deabdd8352019')

prepare() {
  cp -a numpy-$pkgver{,-py2}
  cd numpy-$pkgver-py2

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')

  cd ..
  cd numpy-$pkgver

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python3|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python3|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python3|" \
      -i $(find . -name '*.py')
}

build() {
  cd "$srcdir"/numpy-$pkgver
  python3 setup.py build

  cd "$srcdir"/numpy-$pkgver-py2
  python2 setup.py build
}

check() {
  # TODO: Fix fortran tests here (it works fine after installation)

  cd "$srcdir"/numpy-$pkgver
  python3 setup.py install --root="$PWD/tmp_install" --optimize=1
  cd "$PWD/tmp_install"
  PATH="$PWD/usr/bin:$PATH" PYTHONPATH="$PWD/usr/lib/python3.6/site-packages:$PYTHONPATH" python3 -c 'import numpy; numpy.test()'

  cd "$srcdir"/numpy-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  cd "$PWD/tmp_install"
  PATH="$PWD/usr/bin:$PATH" PYTHONPATH="$PWD/usr/lib/python2.7/site-packages:$PYTHONPATH" python2 -c 'import numpy; numpy.test()'
}

package_python3-numpy() {
  depends=('cblas' 'lapack' 'python3')
  optdepends=('python3-nose: testsuite'
              'openblas: faster linear algebra')

  cd numpy-$pkgver
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python3-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python3-numpy/"
}

package_python2-numpy() {
  depends=('cblas' 'lapack' 'python2')
  optdepends=('python2-nose: testsuite'
              'openblas: faster linear algebra')

  cd numpy-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python2-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python2-numpy/"

  mv "$pkgdir"/usr/bin/f2py{,2}
}
