# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=kscreenlocker
pkgver=5.17.1
pkgrel=1
pkgdesc='Library and components for secure lock screen architecture'
arch=(x86_64)
url='https://projects.kde.org/kscreenlocker'
license=(LGPL)
groups=(plasma)
depends=(kidletime kwayland kdeclarative)
makedepends=(extra-cmake-modules python3 kdoctools kcmutils libxcursor)
options=("debug")
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('56a0ed3284c3ce087d3230789bac5b02505e7ed9795fa37ce9934bc5090673e4'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
} 
