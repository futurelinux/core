# Originally imported from Arch Linux with
# Arch Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Arch Contributor: Daniel Micay <danielmicay@gmail.com>

# Remember to update lib32-rust along with this package.

pkgname=rust
pkgver=1.37.0
pkgrel=1
_llvm_ver=8.0.1
arch=('x86_64')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
provides=('cargo')
conflicts=('cargo')
replaces=('cargo')
depends=('gcc-libs' 'curl' 'libssh2')
makedepends=('rust' "llvm=$_llvm_ver" 'libffi' 'perl' 'python3' 'curl' 'cmake')
options=('!emptydirs')
source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-$_llvm_ver/compiler-rt-$_llvm_ver.src.tar.xz"
        config.toml)
sha256sums=('120e7020d065499cc6b28759ff04153bfdc2ac9b5adeb252331a4eb87cbe38c3'
            '11828fb4823387d820c6715b25f6b2405e60837d12a7469e7a8882911c721837'
            'c2d04b5c71f7c363a2056823a5cf79b6d45dbecfe241f2bc437d2a90806cf302')
options=('!emptydirs')

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  export RUST_BACKTRACE=1
  export RUST_COMPILER_RT_ROOT="$srcdir/compiler-rt-$_llvm_ver.src"

  msg2 "Run build under x.py"
  python3 ./x.py build -j"$(nproc)"
}

package() {
  cd "rustc-$pkgver-src"

  DESTDIR="$pkgdir" python3 ./x.py install

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

  cd "$pkgdir/usr/lib"

  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

  install -d "$pkgdir"/usr/share/bash-completion
  mv "$pkgdir"/etc/bash_completion.d/ "$pkgdir"/usr/share/bash-completion/completions/

  rm rustlib/uninstall.sh # remove uninstall.sh, pacman manages the files anyway
}
