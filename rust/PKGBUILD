# Originally imported from Arch Linux with
# Arch Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Arch Contributor: Daniel Micay <danielmicay@gmail.com>

# Remember to update lib32-rust along with this package.

pkgname=rust
pkgver=1.36.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
provides=('cargo')
conflicts=('cargo')
replaces=('cargo')
depends=('gcc-libs' 'curl' 'libssh2')
makedepends=('rust' 'llvm' 'libffi' 'perl' 'curl' 'cmake' 'ninja' 'python3')
source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"
        config.toml
        librustc-llvm-libffi.patch)
sha256sums=('04c4e4d7213d036d6aaed392841496d272146312c0290f728b7400fccd15bb1b'
            '85dd39356696f8617222f12ab410f94ae2f91e6d14be944a043597f564be1bd4'
            '49fa39e8775c9e3b3400d35d2df0728975371f9832d2d76d0dadf9fc73199774')
options=('!emptydirs')

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
  patch -Np0 -i $srcdir/librustc-llvm-libffi.patch
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python3 ./x.py build -j"$(nproc)"
}

package() {
  cd "rustc-$pkgver-src"

  DESTDIR="$pkgdir" python3 ./x.py install

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

  cd "$pkgdir/usr/lib"

  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

  install -d "$pkgdir"/usr/share/bash-completion
  mv "$pkgdir"/etc/bash_completion.d/ "$pkgdir"/usr/share/bash-completion/completions/

  rm rustlib/uninstall.sh # remove uninstall.sh, pacman manages the files anyway
}
