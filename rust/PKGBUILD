# Originally imported from Arch Linux with
# Arch Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Arch Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=rust
pkgver=1.33.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
provides=('cargo')
conflicts=('cargo')
replaces=('cargo')
depends=('gcc-libs' 'curl' 'libssh2')
makedepends=('rust' 'llvm' 'libffi' 'perl' 'python2' 'curl' 'cmake')
source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"
        config.toml)
sha256sums=('5a01a8d7e65126f6079042831385e77485fa5c014bf217e9f3e4aff36a485d94'
            'ff4890bd733509b0dbb885d69afd578c41fc583af256dcc81e9239339c60b144')
options=('staticlibs' '!strip' '!emptydirs')

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python2 ./x.py build -j"$(nproc)"
}

package() {
  cd "rustc-$pkgver-src"

  DESTDIR="$pkgdir" python2 ./x.py install

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

  cd "$pkgdir/usr/lib"

  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

  install -d "$pkgdir"/usr/share/bash-completion
  mv "$pkgdir"/etc/bash_completion.d/ "$pkgdir"/usr/share/bash-completion/completions/

  rm rustlib/uninstall.sh # remove uninstall.sh, pacman manages the files anyway
}
