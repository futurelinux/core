# Originally imported from Arch Linux with
# Arch Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Arch Contributor: Daniel Micay <danielmicay@gmail.com>

# Remember to update lib32-rust along with this package.

pkgname=rust
pkgver=1.36.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
provides=('cargo')
conflicts=('cargo')
replaces=('cargo')
depends=('gcc-libs' 'curl' 'libssh2')
makedepends=('rust' 'llvm' 'libffi' 'perl' 'python2' 'curl' 'cmake' 'ninja')
source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"
        config.toml)
sha256sums=('04c4e4d7213d036d6aaed392841496d272146312c0290f728b7400fccd15bb1b'
            '3870c9a402fa337bcb0a7c5eaf88c0dc1375578184b4f4742d6899724d3f4a07')
options=('staticlibs' '!strip' '!emptydirs')

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python2 ./x.py build -j"$(nproc)"
}

package() {
  cd "rustc-$pkgver-src"

  DESTDIR="$pkgdir" python2 ./x.py install

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

  cd "$pkgdir/usr/lib"

  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

  install -d "$pkgdir"/usr/share/bash-completion
  mv "$pkgdir"/etc/bash_completion.d/ "$pkgdir"/usr/share/bash-completion/completions/

  rm rustlib/uninstall.sh # remove uninstall.sh, pacman manages the files anyway
}
