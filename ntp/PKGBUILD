pkgname=ntp
_pkgname=ntp #-dev
_pkgver=4.2.8p13
pkgver=${_pkgver/p/.p}
pkgrel=1
pkgdesc='Network Time Protocol reference implementation'
url="http://www.ntp.org/"
license=('custom')
arch=('x86_64')
depends=('openssl' 'libcap' 'libedit')
makedepends=('perl-html-parser')
backup=('etc/ntp.conf')
options=('!emptydirs')
source=("https://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/${_pkgname}-4.2/${_pkgname}-${_pkgver}.tar.gz"
        'ntp.conf'
        'ntpd.service'
        'ntpdate.service'
        'ntp.sysusers')
sha1sums=('cff200a987d64e891fb349a22313ecb0feaea090'
          '76045019df8182c8b759828549dbd957e26c4d6e'
          '0cccca872385e0142888ab48d273fec0669b30a8'
          '059b382d1af0c55202e2d17f2ae065a2cbfec9ee'
          'e8d1c895eabeb45b1a5f096fa5fc3c088eb3ee71')


build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --mandir=/usr/share/man \
    --enable-linuxcaps \
    --enable-ntp-signd
    
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  
  make DESTDIR="${pkgdir}" install
  
  install -d -o 87 "${pkgdir}/var/lib/ntp"
  install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
  install -Dm644 ../ntpd.service "${pkgdir}"/usr/lib/systemd/system/ntpd.service
  install -Dm644 ../ntpdate.service "${pkgdir}"/usr/lib/systemd/system/ntpdate.service
  install -Dm644 ../ntp.sysusers "${pkgdir}"/usr/lib/sysusers.d/ntp.conf
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -d -o 87 "${pkgdir}"/var/lib/ntp
  echo > "${pkgdir}/var/lib/ntp/.placeholder"
}
