# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pep517
pkgname=('python3-pep517')
pkgver=0.8.2
pkgrel=1
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
makedepends=('python3-setuptools' 'python3-toml')
checkdepends=('python3-mock' 'python3-pytest-flake8' 'python3-testpath' 'python3-pip')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('668569a26a4c21a1b8d6828a26bdcdab62b8c4d5fcaba8d9f9a4740e0fce7f071295e28d91349500b4c7be05ab2c2670986de18f01e2afafc1878101e5ef9af2')

build() {
  cd "$srcdir"/pep517-$pkgver
  python3 setup.py build
}

check() {
  cd pep517-$pkgver
  # https://github.com/pypa/pep517/issues/35
  pytest --deselect setup.py
}

package_python3-pep517() {
  depends=('python3-toml')

  cd pep517-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
