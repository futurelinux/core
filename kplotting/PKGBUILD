# Contributor: Andrea Scarpino <andrea@archlinux.org>


pkgname=kplotting
pkgver=5.61.0
pkgrel=1
pkgdesc='Lightweight plotting framework'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kplotting'
license=('LGPL')
depends=("qt5-base")
makedepends=("extra-cmake-modules>=5.61.0")
checkdepends=('xorg-server-xvfb')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('95781b50bef0e081e48b472b4fcbbcd3301ec45245498261e4a3ec8e42b892ba'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

# check() {
#   cd build
#   xvfb-run make test
# }

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
