
pkgname=qt5-quickcontrols
pkgver=5.12.2
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Reusable Qt Quick based UI controls to create classic desktop-style user interfaces'
depends=('qt5-declarative')
makedepends=()
groups=('qt5')
options=('debug')
_pkgfqn="${pkgname/5-/}-everywhere-src-${pkgver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/$pkgver/submodules/${_pkgfqn}.tar.xz"
               qtbug-73961.patch)
md5sums=('469629d68419c135f77f760aa0e06da4'
         '580a78ed7e35993a0a1ec5f6c6db6812')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../qtbug-73961.patch # Fix OOM with some Plasma themes https://bugreports.qt.io/browse/QTBUG-73691
}

build() {
  cd build

  qmake-qt5 ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
