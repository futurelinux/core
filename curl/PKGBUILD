# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Pierre Schmitz <pierre@archlinux.de>
#               Dave Reisner <dreisner@archlinux.org>
#               Angel Velasquez <angvp@archlinux.org>
#               Eric Belanger <eric@archlinux.org>
#               Lucien Immink <l.immink@student.fnt.hvu.nl>
#               Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=curl
pkgver=7.69.1
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('ca-certificates'
         'krb5'
         'libssh2'
         'openssl'
         'zlib'
         'libpsl'
         'libnghttp2'
         'libidn2'
         'libidn2.so')
provides=('libcurl.so')
source=("https://curl.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('c0bc5e52fd3f52e095f61cf2724de57f2698317dd19ca8c331575f3998d08b067adefcb57dc5274747276a5556df89465fe13976774af885804fa81ca417887f'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-ldap \
      --disable-ldaps \
      --disable-manual \
      --enable-ipv6 \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --with-gssapi \
      --with-libssh2 \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install -C scripts

  # License
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
