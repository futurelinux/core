# Contributions from Arch:
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dav1d
pkgver=0.3.1
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm' 'doxygen')
provides=('libdav1d.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('be511d465223cc74aab507fe3a6e4e764501662ebf1a5e233854ed064a7ae204')

build() {
  cd ${pkgbase}-${pkgver}
  chakra-meson build
  ninja -C build
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
