# Contributions from Arch:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: TingPing <tingping@tingping.se>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgbase=brotli
pkgname=('brotli' 'python3-brotli' 'python2-brotli' 'brotli-testdata')
pkgver=1.0.7
pkgrel=1
pkgdesc='Brotli compression library'
arch=('x86_64')
license=('MIT')
url='https://github.com/google/brotli'
depends=('gcc-libs')
makedepends=('cmake' 'python3-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/$pkgbase/archive/v$pkgver.tar.gz")
sha256sums=('4c61bfb0faca87219ea587326c467b95acb25555b53d1a421ffa3c8a9296ee2c')

prepare() {
  cp -a brotli-$pkgver{,-py2}
  mkdir -p build
}

build() {
  cd "$srcdir"/brotli-$pkgver
  python3 setup.py build

  cd "$srcdir"/brotli-$pkgver-py2
  python2 setup.py build

  cd "$srcdir"/build
  cmake ../brotli-$pkgver -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_INSTALL_LIBDIR="/usr/lib"
  make
}

package_brotli() {
  cd build
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python3-brotli() {
  depends=('python3')

  cd brotli-$pkgver

  python3 setup.py install --skip-build -O1 --root="$pkgdir"
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-brotli() {
  depends=('python2')

  cd brotli-$pkgver-py2

  python2 setup.py install --skip-build -O1 --root="$pkgdir"
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_brotli-testdata() {
  depends=()

  cd brotli-$pkgver
  install -dm755 "$pkgdir"/usr/share/brotli
  cp -a tests/testdata "$pkgdir"/usr/share/brotli/
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
