# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=prison
pkgver=5.63.0
pkgrel=2
arch=('x86_64')
pkgdesc="A barcode api to produce QRCode barcodes and DataMatrix barcode"
url="https://projects.kde.org/projects/kdesupport/prison"
license=('GPL' 'LGPL' 'FDL')
makedepends=('cmake' 'extra-cmake-modules')
depends=('qt5-base' 'libdmtx' 'qrencode')
replaces=('prison-kf5')
provides=('prison-kf5')
conflicts=('prison-kf5')
groups=('kf5')
options=("debug")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('85ffbf88aa02bbecadd88572f035c72c932d3329a0aedcc91994572c64a9f36a'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
