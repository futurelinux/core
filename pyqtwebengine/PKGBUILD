# Contributions from Arch:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqtwebengine
pkgname=(python3-pyqtwebengine pyqtwebengine-common)
pkgver=5.13.1
pkgrel=1
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtwebengine/intro"
license=(GPL)
makedepends=(sip qt5-webengine python3-pyqt5)
source=("https://www.riverbankcomputing.com/static/Downloads/PyQtWebEngine/${pkgver}/PyQtWebEngine_gpl-$pkgver.tar.gz")
sha256sums=('8d8c1262005d8465653a848bf67327fb338e0d3c2d26090a6f7eb071dbb42092')

build() {
  cd "$srcdir"/PyQtWebEngine_gpl-$pkgver
  python3 configure.py \
    --no-sip-files \
    -q /usr/bin/qmake-qt5
  make
}

package_pyqtwebengine-common(){
  pkgdesc="Common files shared between python-pyqtwebengine and python2-pyqtwebengine"
  depends=(qt5-webengine)

  cd PyQtWebEngine_gpl-$pkgver
  install -Dm644 PyQtWebEngine.api -t "$pkgdir"/usr/share/qt/qsci/api/python

  install -d "$pkgdir"/usr/share/sip/PyQt5
  cp -a sip/* "$pkgdir"/usr/share/sip/PyQt5
}

package_python3-pyqtwebengine(){
  pkgdesc="Python 3 bindings for QtWebEngine"
  depends=(python3-pyqt5 pyqtwebengine-common)

  cd PyQtWebEngine_gpl-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1

  # Provided by pyqtwebengine-common
  rm -r "$pkgdir"/usr/share
}
