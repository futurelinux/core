pkgbase=projectm
pkgname=('projectm' 'projectm-sdl' 'projectm-pulseaudio')
pkgver=3.1.2
pkgrel=1
arch=('x86_64')
url='https://github.com/projectM-visualizer/projectm/'
license=('LGPL')
makedepends=('mesa-libgl' 'glm' 'qt5-base' 'ftgl' 'glew' 'sdl2' 'libpulse')
source=("https://github.com/projectM-visualizer/$pkgbase/releases/download/v$pkgver/projectM-src-$pkgver.tar.gz")
sha256sums=('8709d7bce65c78d56a546820762a6ede6d7c144695fd1d200e972f38740e9db9')

build() {
    cd projectM-$pkgver
    ./configure --prefix=/usr --enable-sdl --enable-threading --enable-qt
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
    make
}

package_projectm() {
    pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering'
    depends=('libgl' 'glm' 'ftgl' 'glew')


    cd "$srcdir/projectM-$pkgver"
    make DESTDIR="$pkgdir" install-data-am
    cd "$srcdir/projectM-$pkgver/src"
    make DESTDIR="$pkgdir" install-am
    cd "$srcdir/projectM-$pkgver/src/libprojectM"
    make DESTDIR="$pkgdir" install
    cd "$srcdir/projectM-$pkgver/src/NativePresets"
    make DESTDIR="$pkgdir" install

    find "$pkgdir" -iname "\.*" -delete
}

package_projectm-sdl() {
    pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering (sdl)'
    depends=('projectm' 'sdl2')

    cd "$srcdir/projectM-$pkgver/src/projectM-sdl"
    make DESTDIR="$pkgdir" install
}

package_projectm-pulseaudio() {
    pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering (pulseaudio)'
    depends=('projectm' 'qt5-base' 'libpulse')

    cd "$srcdir/projectM-$pkgver/src/projectM-pulseaudio"
    make DESTDIR="$pkgdir" install
}
