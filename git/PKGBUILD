# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Christian Hesse <mail@eworm.de>
#               Dan McGee <dan@archlinux.org>

pkgname=git
pkgver=2.26.0
pkgrel=1
pkgdesc='Distributed version-control system for tracking changes in source code during software development'
arch=('x86_64')
url="https://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat' 'perl' 'perl-error' 'perl-mailtools' 'openssl' 'pcre2' 'grep' 'shadow')
makedepends=('python3' 'asciidoc' 'xmlto')
optdepends=('org.freedesktop.secrets: keyring credential helper'
            'libsecret: libsecret credential helper'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn and interactive.singlekey setting'
            'perl-net-smtp-ssl: git send-email TLS support'
            'perl-mime-tools: git send-email'
            'perl-authen-sasl: git send-email TLS support'
            'perl-mediawiki-api: git mediawiki support'
            'perl-datetime-format-iso8601: git mediawiki support'
            'perl-lwp-protocol-https: git mediawiki https support'
            'perl-cgi: gitweb (web interface) support'
            'python3: git svn & git p4'
            'subversion: git svn'
            'tk: gitk and git gui')
install="git.install"
validpgpkeys=('96E07AF25771955980DAD10020D04E5A713660A7') # Junio C Hamano
source=("https://www.kernel.org/pub/software/scm/git/git-$pkgver.tar"{.xz,.sign}
        '0001-git-p4-python.patch'
        'git-daemon@.service'
        'git-daemon.socket'
        'git-sysusers.conf')
sha256sums=('9ece0dcb07a5e0d7366a92b613b201cca11ae368ab7687041364b3e756e495d6'
            'SKIP'
            '0d19345aaeaeb374f8bfc30b823e8f53cb128c56b68c6504bbdd8766c03d1df9'
            '14c0b67cfe116b430645c19d8c4759419657e6809dfa28f438c33a005245ad91'
            'ac4c90d62c44926e6d30d18d97767efc901076d4e0283ed812a349aece72f203'
            '7630e8245526ad80f703fac9900a1328588c503ce32b37b9f8811674fcda4a45')

_make_paths=(
  prefix='/usr'
  gitexecdir='/usr/lib/git-core'
  perllibdir="$(/usr/bin/perl -MConfig -wle 'print $Config{installvendorlib}')"
)

_make_options=(
  CFLAGS="$CFLAGS"
  LDFLAGS="$LDFLAGS"
  NO_CROSS_DIRECTORY_HARDLINKS=1
  MAN_BOLD_LITERAL=1
  NO_PERL_CPAN_FALLBACKS=1
  USE_LIBPCRE2=1
)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 <../0001-git-p4-python.patch
}

build() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "${srcdir}/${pkgname}-${pkgver}"

  make \
    "${_make_paths[@]}" \
    "${_make_options[@]}" \
    all man

  make -C contrib/credential/libsecret
  make -C contrib/subtree "${_make_paths[@]}" all man
  make -C contrib/mw-to-git "${_make_paths[@]}" all
  make -C contrib/diff-highlight "${_make_paths[@]}"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  local jobs
  jobs=$(expr "$MAKEFLAGS" : '.*\(-j[0-9]*\).*') || true
  mkdir -p /dev/shm/git-test
  # explicitly specify SHELL to avoid a test failure in t/t9903-bash-prompt.sh
  # which is caused by 'git rebase' trying to use builduser's SHELL inside the
  # build chroot (i.e.: /usr/bin/nologin)
  SHELL=/bin/sh \
    make \
    "${_make_paths[@]}" \
    "${_make_options[@]}" \
    NO_SVN_TESTS=y \
    DEFAULT_TEST_TARGET=prove \
    GIT_PROVE_OPTS="$jobs -Q" \
    GIT_TEST_OPTS="--root=/dev/shm/git-test" \
    test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make \
    "${_make_paths[@]}" \
    "${_make_options[@]}" \
    DESTDIR="$pkgdir" \
    install install-man

  # bash completion
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  install -m644 ./contrib/completion/git-completion.bash "${pkgdir}/usr/share/bash-completion/completions/git"
  # fancy git prompt
  mkdir -p "${pkgdir}/usr/share/git/"
  install -m644 ./contrib/completion/git-prompt.sh "${pkgdir}/usr/share/git/git-prompt.sh"
  # libsecret credentials helper
  install -m 0755 contrib/credential/libsecret/git-credential-libsecret \
    "$pkgdir"/usr/lib/git-core/git-credential-libsecret
  make -C contrib/credential/libsecret clean
  # subtree installation
  make -C contrib/subtree "${_make_paths[@]}" DESTDIR="$pkgdir" install install-man
  # the rest of the contrib stuff
  find contrib/ -name '.gitignore' -delete
  cp -a ./contrib/* "$pkgdir"/usr/share/git/

  # scripts are for python 2.x
  sed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    $(find "$pkgdir" -name '*.py') \
    "$pkgdir"/usr/share/git/remote-helpers/git-remote-bzr \
    "$pkgdir"/usr/share/git/remote-helpers/git-remote-hg
  sed -i 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
    "$pkgdir"/usr/share/git/svn-fe/svnrdump_sim.py

  # git-daemon via systemd socket activation
  install -D -m 0644 "$srcdir"/git-daemon@.service "$pkgdir"/usr/lib/systemd/system/git-daemon@.service
  install -D -m 0644 "$srcdir"/git-daemon.socket "$pkgdir"/usr/lib/systemd/system/git-daemon.socket

  # sysusers file
  install -D -m 0644 "$srcdir"/git-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/git.conf
}
