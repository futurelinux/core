pkgname=qpdf
pkgver=8.4.0
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="https://github.com/qpdf/qpdf"
license=('custom:Artistic-2.0' 'Apache')
depends=('libjpeg-turbo' 'zlib')
optdepends=('perl: required for fix-qdf')
checkdepends=('perl')
source=("https://github.com/qpdf/qpdf/releases/download/release-${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('b16325a88a09ed4b0684e569f060c406f8b208ef')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 completions/bash/qpdf "${pkgdir}/usr/share/bash-completion/completions/qpdf"
  install -Dm644 completions/zsh/_qpdf "${pkgdir}/usr/share/zsh/site-functions/_qpdf"

  mkdir -m755 -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 {Artistic-2.0,LICENSE.txt} ${pkgdir}/usr/share/licenses/${pkgname}/
}
