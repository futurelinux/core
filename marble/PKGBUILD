# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>
#               Gustavo Alvarez <sl1pkn07@gmail.com>
#               Balló György <ballogyor+arch@gmail.com>

pkgbase=marble
pkgname=(marble-common marble-qt marble)
pkgver=19.12.3
pkgrel=3
pkgdesc="Desktop Globe"
arch=(x86_64)
url="https://www.kde.org/applications/system/marble"
license=(GPL)
makedepends=(extra-cmake-modules gpsd kdoctools knewstuff kparts krunner libwlocate phonon-qt5
             plasma-workspace qt5-serialport qt5-tools qt5-webengine shapelib)
options=('docs' 'debug')
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgbase-$pkgver.tar.xz"{,.sig})
sha256sums=('73a2c5234f8a1728e2a97499166e7bbf8dfb2f48d10fe8cff96380631d064627'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7 # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_LIBDIR=lib \
    -DQT_PLUGINS_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TESTS=OFF \
    -DBUILD_MARBLE_TOOLS=ON \
    -DMOBILE=OFF
  make
}

package_marble-common() {
  pkgdesc='Common libraries and plugins for Marble'
  depends=(qt5-svg qt5-webengine phonon-qt5 desktop-file-utils)
  optdepends=('gpsd: GPS based geolocation'
              'libwlocate: WLAN based geolocation'
              'qt5-serialport: APRS plugin'
              'shapelib: SHP plugin')

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/share/{config.kcfg,kxmlgui5,metainfo,plasma} \
        "$pkgdir"/usr/bin \
        "$pkgdir"/etc \
        "$pkgdir"/usr/lib/qt/{qml,plugins/*.so} \
        "$pkgdir"/usr/share/applications/{marble_geo.desktop,marble_worldwind.desktop,org.kde.marble*.desktop} \
        "$pkgdir"/usr/share/kservices5/{marble_part.desktop,plasma*} \
        "$pkgdir"/usr/share/locale/*/LC_MESSAGES/*.mo
}

package_marble-qt() {
  pkgdesc+=' (Qt version)'
  depends=(marble-common)

  cd build
  make -C src/apps/marble-qt DESTDIR="$pkgdir" install
}

package_marble() {
  depends=(marble-common knewstuff kparts)
  optdepends=('krunner: Krunner plugin')

  cd build
  make -C src/apps/marble-kde DESTDIR="$pkgdir" install
  make -C src/plasma DESTDIR="$pkgdir" install
  make -C src/plasmarunner DESTDIR="$pkgdir" install
  make -C src/thumbnailer DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/share/{icons,doc/HTML}
}
