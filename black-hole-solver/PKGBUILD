# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Antonio Rojas <arojas@archlinux.org>

pkgname=black-hole-solver
pkgver=1.6.0
pkgrel=3
pkgdesc="Solvers and statistics for 'Golf' solitaire, 'Black Hole' solitaire, 'All in a Row' solitaire and related card patience games."
arch=(x86_64)
url="https://www.shlomifish.org/open-source/projects/black-hole-solitaire-solver/"
license=(MIT)
depends=(glibc)
makedepends=(cmake perl-path-tiny rinutils)
source=(https://fc-solve.shlomifish.org/downloads/fc-solve/black-hole-solver-$pkgver.tar.xz)
sha256sums=('2c72ef77116276324c08e237c2abb940761925433f6283a184d1aeb9428e9341')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_STATIC_LIBRARY=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
