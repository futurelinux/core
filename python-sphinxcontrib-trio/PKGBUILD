# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python3-sphinxcontrib-trio
_pyname=${pkgname/python3-/}
pkgver=1.1.0
pkgrel=3
pkgdesc='Make Sphinx better at documenting Python functions and methods'
url='https://github.com/python3-trio/sphinxcontrib-trio'
arch=('any')
license=('MIT')
depends=('python' 'python3-sphinx')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest' 'python3-lxml' 'python3-cssselect')
source=(https://github.com/python3-trio/sphinxcontrib-trio/archive/v${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('0d119cb8a1669dcb10f4498d87dae70496b64976618783c7b49f8177b03eb15d7fb8833f21b395f62288e3e619dd98a620677d316bafbcc0bc716115a9b574c2')

build() {
  cd ${_pyname}-${pkgver}
  python3 setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=. py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python3 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
