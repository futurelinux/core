# Part of the X.org group
# Contributions from Arch for all the related packages listed under source: https://www.archlinux.org/packages/

pkgname=xorg-apps
pkgver=7.6
pkgrel=7
pkgdesc="Various X.Org applications"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxcursor' 'libxkbfile' 'libpng' 'libxft' 'libfontenc' 'libxaw' 'libfontenc' 'libx11' 'libxmu'
         'libxext' 'libxrender' 'libxt' 'libsm' 'graphite')
makedepends=('pkgconfig' 'xbitmaps' 'xorg-util-macros' 'xproto')
source=(luit-fix.diff
        ${url}/releases/individual/app/luit-1.1.1.tar.bz2
	${url}/releases/individual/app/oclock-1.0.4.tar.bz2
        ${url}/releases/individual/app/x11perf-1.6.1.tar.bz2
        ${url}/releases/individual/app/xbiff-1.0.4.tar.bz2
        ${url}/releases/individual/app/xcalc-1.1.0.tar.bz2
        ${url}/releases/individual/app/xclipboard-1.1.3.tar.bz2
        ${url}/releases/individual/app/xclock-1.0.9.tar.bz2
        ${url}/releases/individual/app/xconsole-1.0.7.tar.bz2
        ${url}/releases/individual/app/xcursorgen-1.0.7.tar.bz2
        ${url}/releases/individual/app/xedit-1.2.2.tar.bz2
        ${url}/releases/individual/app/xeyes-1.1.2.tar.bz2
        ${url}/releases/individual/app/xkill-1.0.5.tar.bz2
        ${url}/releases/individual/app/xload-1.1.3.tar.bz2
        ${url}/releases/individual/app/xlogo-1.0.5.tar.bz2
        ${url}/releases/individual/app/xmag-1.0.6.tar.bz2
        ${url}/releases/individual/app/xman-1.1.5.tar.bz2
        ${url}/releases/individual/app/xmessage-1.0.5.tar.bz2
        ${url}/releases/individual/app/xpr-1.0.5.tar.bz2
        ${url}/releases/individual/app/xwd-1.0.7.tar.bz2
        ${url}/releases/individual/app/xwud-1.0.5.tar.bz2
        LICENSE)
md5sums=('7db0193581913adbcf117276980689a2'
         'c4a3664e08e5a47c120ff9263ee2f20c'
         '973f6d25a6f0990720b98a15c9436b92'
         'e96b56756990c56c24d2d02c2964456b'
         '66dd3ebd6351b1911b831b89b2dba8ec'
         '3b566e47c76356cecaa71eb431767ae1'
         '3facff108f65b0733b04becf20274e45'
         '437522a96f424f68fc64ed34ece9b211'
         '5c42e161fcd4289a463366e17683598a'
         '25cc7ca1ce5dcbb61c2b471c55e686b5'
         '74ee6db901e81978b087b2026d8f30a0'
         '6f0543ec84283df5743eeafc173bea4a'
         '938177e4472c346cf031c1aefd8934fc'
         '29afafdffe97f809d43a41db9dd63f25'
         'e9d21de1dc8c8b9f854509a26a985cd7'
         '8aaa41374935d697ee55d7dc9de70781'
         '629436867b202098967636b17c8d303c'
         'e50ffae17eeb3943079620cb78f5ce0b'
         'eaac255076ea351fd08d76025788d9f9'
         '11794a8eba6d295a192a8975287fd947'
         '79972093bb0766fcd0223b2bd6d11932'
         '1a3977bcff6dc0fc1086e0650fcea31a')
         
prepare() {
  cd "${srcdir}/luit-1.1.1"
  patch -Np1 -i $srcdir/luit-fix.diff
  autoreconf -vfi
}

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man \
        --with-localealiasfile=/usr/share/X11/locale/locale.alias
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
