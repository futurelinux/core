# Maintainer:   totte <totte@chakralinux.org>
# Contributors: St√©phane Gaudreault <stephane@archlinux.org>
#               Tobias Powalowski <tpowa@archlinux.org>
#               Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
# Commit date + git rev-parse --short origin/master
_rev=13e0084
pkgver=20190709
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
license=('GPL')
url="http://www.skbuff.net/iputils/"
groups=('base')
depends=('openssl' 'libcap' 'libidn2')
optdepends=('xinetd: for tftpd')
makedepends=('perl-sgmls' 'git' 'docbook-xsl' 'meson')
backup=(etc/xinetd.d/tftp)
install=${pkgname}.install
source=("git+https://github.com/iputils/iputils.git#commit=${_rev}"
        tftp.xinetd
        fix-setuid-redeclared.patch)
sha1sums=('SKIP'
          'fc2ae26f5609725e3f4aeaf4ab82dfa6d2e378fd'
          'ea7c400d1c397d514de718957c28730d87cef656')

prepare() {
  mkdir -p build
  cd $pkgname
  patch -Np1 -i $srcdir/fix-setuid-redeclared.patch
}

build() {
  cd build
  chakra-meson ../$pkgname -DBUILD_RARPD=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  # FS#24768
  install -dm755 "${pkgdir}"/etc/xinetd.d/
  install -m644 "${srcdir}"/tftp.xinetd "${pkgdir}"/etc/xinetd.d/tftp
}
