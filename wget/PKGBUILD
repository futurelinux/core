pkgname=wget
pkgver=1.20.3
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base')
depends=('openssl' 'libidn2' 'util-linux' 'pcre2')
checkdepends=('perl-http-daemon' 'perl-io-socket-ssl' 'python3')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('db4e6dc7977cbddcd543b240079a4899'
         'SKIP')
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA'
              '7845120B07CBD8D6ECE5FF2B2A1743EDA91A35B6'
              '1CB27DBC98614B2D5841646D08302DB6A2670428') # Tim RÃ¼hsen <tim.ruehsen@gmx.de>

prepare() {
  cd ${pkgname}-${pkgver}
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-nls --with-ssl=openssl
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check | true
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
