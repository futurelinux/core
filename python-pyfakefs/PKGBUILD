# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python3-pyfakefs
pkgver=4.0.2
pkgrel=1
pkgdesc='pyfakefs implements a fake file system that mocks the Python file system modules'
url=https://github.com/jmcgeheeiv/pyfakefs/
license=(APACHE)
arch=(any)
depends=(python3-pytest)
makedepends=(
  git
  python3-setuptools
)
checkdepends=(
  python3-tox
)
source=(git+https://github.com/jmcgeheeiv/pyfakefs.git#tag=c0a5d83f6504cbac50c8bc6a923f4f2eda194c76)
sha256sums=(SKIP)

pkgver() {
  cd pyfakefs

  git describe --tags | sed 's/^v//'
}

build() {
  cd  pyfakefs

  python3 setup.py build
}

check() {
  cd  pyfakefs

  tox -e py38
}

package() {
  cd pyfakefs

  python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
