# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=extra-cmake-modules
pkgver=5.68.0
pkgrel=2
pkgdesc='Extra CMake modules'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('cmake')
makedepends=('python3-requests' 'python3-sphinx' 'qt5-tools' )
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        ECM-no-init.py.patch)
sha256sums=('4d60869ca96a323b56f00b40c4728a70dfebe2132bbae040442a6a2ef90e2d6e'
            'SKIP'
            'ca89ed6c7e802afacd05a25081948f49ca278dc361848df506f39d11951fbca2')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../ECM-no-init.py.patch # Don't create __init__.py
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_HTML_DOCS=OFF \
    -DBUILD_QTHELP_DOCS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
