# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=pyside2-tools
_qtver=5.12.6
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io/'
license=(LGPL)
pkgdesc='lupdate, rcc and uic tools for PySide2'
depends=('pyside2')
makedepends=('cmake' 'shiboken2')
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('53379f43891a6bc72fc344f5963d77de0916344637132aae634677dfbba185d8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/pyside2-tools \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DCMAKE_BUILD_TYPE=None # https://bugreports.qt.io/browse/PYSIDE-980
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  #rm "$pkgdir"/usr/bin/{rcc,uic} # provided by qt5-base
}
