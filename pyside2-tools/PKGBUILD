# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=pyside2-tools
_qtver=5.14.1
pkgver=${_qtver/-/}
pkgrel=3
arch=(x86_64)
url='https://www.qt.io/'
license=(LGPL)
pkgdesc='Tools for PySide2'
depends=('pyside2')
makedepends=('cmake' 'shiboken2')
_pkgfqn=pyside-setup-opensource-src-${_qtver}
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('41ce931695567639c92acb68a9d66ed5609f067011af9a94b53fc0d697ad1d1c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/pyside2-tools \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/bin/rcc "$pkgdir"/usr/bin/pyside2-rcc
  mv "$pkgdir"/usr/bin/uic "$pkgdir"/usr/bin/pyside2-uic
}
