# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Antonio Rojas <arojas@archlinux.org>
#               Sergej Pupykin <pupykin.s+arch@gmail.com>
#               Fabian Schoelzel <myfirstname.mylastname@googlemail.com>
#               funkyou <spamopfer@nickname.berlin.de>
#               tardo <tardo@nagi-fanboi.net>
#               Stefan Husmann <stefan-husmann@t-online.de>
#               Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=mlt
pkgver=6.20.0
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=(GPL)
depends=(libebur128)
makedepends=(ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg jack libexif python3 swig movit eigen3 opencv rubberband)
optdepends=('sdl_image: SDL1 plugin'
            'sdl2: SDL2 plugin'
            'libsamplerate: libavresample plugin'
            'sox: SOX (Audio Swiss Army Knife) plugin'
            'ffmpeg: ffmpeg plugin'
            'vid.stab: video stabilize plugin'
            'gtk2: Gtk plugin'
            'qt5-svg: Qt5 plugins'
            'jack: JACK sound output plugin'
            'ladspa: LADSPA plugins'
            'libexif: auto rotate plugin'
            'frei0r-plugins: for additional effects'
            'movit: opengl plugin'
            'opencv: openCV plugin'
            'rubberband: rubberband plugin'
            'python3: python bindings')
source=($pkgname-$pkgver.tar.gz::"https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz")
sha256sums=('ab211e27c06c0688f9cbe2d74dc0623624ef75ea4f94eea915cdc313196be2dd')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd mlt-$pkgver

  ./configure \
    --prefix=/usr \
    --avformat-swscale \
    --enable-gpl \
    --enable-gpl3 \
    --enable-opencv \
    --disable-gtk2
  make

  # python bindings
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  sed -i 's_path=`which python_path=`which python3_' build
  sed -i 's_`python -c_`python3 -c_' build
  sed -i 's#python-config#python3-config#' build
  sed -i 's|python{}.{}|python{}.{}m|' build # Fix Python 3 include dir
  ./build
}

package() {
  cd mlt-$pkgver
  make DESTDIR="$pkgdir" install

  # Install python bindings
  _pythonpath=`python3 -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/$_pythonpath"
  install -m755 mlt.py "$pkgdir/$_pythonpath"
  install -m755 _mlt.so "$pkgdir/$_pythonpath"
}
