# Maintainer:   H W Tovetj√§rn (totte) <totte@tott.es>
# Contributors: AlmAck

pkgbase=mlt
pkgname=(mlt python3-mlt)
pkgver=6.16.0
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=(GPL)
makedepends=(ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg jack libexif python3 swig movit eigen3 libebur128)
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz")
md5sums=('9b917594f44132cd459c18dfc113352e')
options=('debug')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  # mlt
  cd "$srcdir/mlt-$pkgver"

   #--avformat-vdpau \ bug: https://sourceforge.net/p/mlt/bugs/240/
   #https://bugs.gentoo.org/show_bug.cgi?id=580630

  ./configure --prefix=/usr \
              --avformat-swscale \
              --enable-gpl --enable-gpl3 \
              --enable-opencv \
              --qt-includedir=/usr/include/qt5 \
              --qt-libdir=/usr/lib \
              --disable-gtk2
  make

  # mlt python bindings
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  sed -i 's_path=`which python_path=`which python3_' build
  sed -i 's_`python -c_`python3 -c_' build
  sed -i 's#python-config#python3-config#' build
  sed -i 's|python{}.{}|python{}.{}m|' build # Fix Python 3 include dir
  ./build
}

package_mlt() {
  depends=(libebur128)
  optdepends=('sdl_image: for SDL plugin'
            'libsamplerate: for libavresample plugin'
            'sox: for SOX (Audio Swiss Army Knife) plugin'
            'ffmpeg: for ffmpeg plugin'
            'vid.stab: for video stabilize plugin'
            'qt5-svg: for Qt5 plugins'
            'jack2: for JACK sound output plugin'
            'ladspa: for LADSPA plugins'
            'libexif: for auto rotate plugin'
            'frei0r-plugins: for additional effects'
            'movit: opengl plugin'
            'opencv: openCV plugin')

  cd "$srcdir/mlt-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_python3-mlt() {
  depends=(python3 mlt)
  conflicts=(mlt-python-bindings)
  provides=(mlt-python-bindings)
  replaces=(mlt-python-bindings)

  _pythonpath=`python3 -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/$_pythonpath"
  install -m755 mlt.py "$pkgdir/$_pythonpath"
  install -m755 _mlt.so "$pkgdir/$_pythonpath"
}
