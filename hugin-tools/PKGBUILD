pkgname=hugin-tools
_pkgname=hugin
pkgver=2019.2.0
pkgrel=1
pkgdesc="Hugin libs and tools to be used by Kipi-plugins"
arch=('x86_64')
url="http://hugin.sourceforge.net/"
license=('LGPL')
depends=('boost-libs' 'enblend-enfuse' 'exiv2>=0.25' 'fftw' 'perl-exiftool' 'openexr' 'libpano13>=2.9.19'
         'sqlite3' 'lapack' 'glew' 'desktop-file-utils' 'icu' 'libxt' 'libpng' 'libxrandr' 'openexr')
makedepends=('zip' 'cmake' 'boost' 'mesa' 'tclap' 'swig' )
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"
            noWxWidgets.patch
            noWxWidgets2.patch)
sha1sums=('e91bfbb286d0d5f40cd93ecb961eb55a37f3f5bb'
          '030149781f5589c088854dac0a4617baf433fe9f'
          '35c22f214fe4b27f5a74c2e7f098516a3759ed6d')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  patch -Np1 -i ../noWxWidgets.patch
  patch --binary -Np1 -i ../noWxWidgets2.patch
}

build() {
  cd "${srcdir}"

  mkdir -p build 
  cd build

  # this makes possible to compile all the hugin tools without GUis
  sed -i -e "s~FIND_PACKAGE(wxWidgets REQUIRED)~~g" ${srcdir}/${_pkgname}-${pkgver}/CMakeLists.txt
  #mkdir -p ${srcdir}/${_pkgname}-${pkgver}/src/hugin_base/PT

  cmake "${srcdir}/${_pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
    -DENABLE_LAPACK=yes \
    -DBUILD_HSI=no
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  # Remove frontend files
  rm -rf -v "${pkgdir}"/usr/share/applications
  rm -rf -v "${pkgdir}"/usr/share/man
  rm -rf -v "${pkgdir}"/usr/share/locale
}
