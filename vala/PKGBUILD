pkgname=vala
pkgver=0.44.6
pkgrel=1
pkgdesc="Compiler for the GObject type system."
arch=('x86_64')
url="https://wiki.gnome.org/Projects/Vala"
license=('LGPL')
depends=('glib2' 'graphviz' 'ttf-font' 'pkg-config' 'gcc')
makedepends=('libxslt' 'gtk-doc' 'help2man' 'autoconf-archive')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
source=(https://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('ab9f1756af7460aa1e39c9a7388f5492a4252a3cd9c76b02f2033f1dedcd793a')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd $pkgname-$pkgver
    make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
