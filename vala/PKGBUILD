pkgname=vala
pkgver=0.42.6
pkgrel=1
pkgdesc="Compiler for the GObject type system."
arch=('x86_64')
url="https://wiki.gnome.org/Projects/Vala"
license=('LGPL')
depends=('glib2' 'graphviz' 'ttf-font' 'pkg-config' 'gcc')
makedepends=('libxslt' 'gtk-doc' 'help2man')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
source=(https://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('3774f46fed70f528d069beaa2de5eaeafa2851c3509856dd10030fa1f7230290')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd $pkgname-$pkgver
    make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
