# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>

pkgname=qt5-webkit
_pkgver=5.212.0-alpha4
pkgver=${_pkgver/-/}
pkgrel=3
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
depends=('qt5-location'
         'qt5-sensors'
         'qt5-webchannel'
         'libwebp'
         'libxslt'
         'libxcomposite'
         'gst-plugins-base'
         'hyphen'
         'woff2')
makedepends=('cmake'
             'ruby'
             'gperf'
             'python2'
             'qt5-doc'
             'qt5-tools')
optdepends=('gst-plugins-good: Webm codec support')
groups=(qt5)
source=("https://github.com/qtwebkit/qtwebkit/releases/download/qtwebkit-$_pkgver/qtwebkit-$_pkgver.tar.xz")
sha256sums=('9ca126da9273664dd23a3ccd0c9bebceb7bb534bddd743db31caf6a5a6d4a9e6')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../qtwebkit-$_pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
