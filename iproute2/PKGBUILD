pkgname=iproute2
pkgver=4.19.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="https://git.kernel.org/pub/scm/network/iproute2/iproute2.git"
depends=('glibc' 'iptables' 'libelf')
makedepends=('linux-atm')
optdepends=('linux-atm: ATM support')
groups=('base')
provides=('iproute')
conflicts=('iproute')
replaces=('iproute')
options=('staticlibs')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=("http://www.kernel.org/pub/linux/utils/net/$pkgname/$pkgname-$pkgver.tar.xz"
        iproute2-fhs.patch)
sha256sums=('d9ec5ca1f47d8a85416fa26e7dc1cbf5d067640eb60e90bdc1c7e5bdc6a29984'
            '7ab86ebccbdbd2e0218400588a3e6a8404e2ee0004794dacba2a01256540150e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # set correct fhs structure
  patch -Np1 -i "$srcdir/iproute2-fhs.patch"

  # do not treat warnings as errors
  sed -i 's/-Werror//' Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" SBINDIR="/usr/bin" install

  # libnetlink isn't installed, install it FS#19385
  install -Dm644 include/libnetlink.h "$pkgdir/usr/include/libnetlink.h"
  install -Dm644 lib/libnetlink.a "$pkgdir/usr/lib/libnetlink.a"
}
