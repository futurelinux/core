pkgname=subversion
pkgver=1.14.0
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('x86_64')
url="https://subversion.apache.org/"
license=('apache')
depends=('sqlite' 'libutf8proc' 'file' 'serf' 'systemd')
makedepends=('apache' 'python' 'perl' 'swig' 'ruby' 'java-runtime' 
             'autoconf' 'kwallet' 'kdelibs4support' 'openjdk' 'python3-py3c')
optdepends=('kwallet: for KWallet for auth credentials'
            'bash-completion: for svn bash completion'
            'python3: for some hook scripts'
            'java-environment: for Java support'
            'ruby: for some hook scripts')
backup=('etc/xinetd.d/svn' 'etc/conf.d/svnserve')
provides=('svn')
options=('!makeflags' '!emptydirs')
source=(https://www.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2{,.asc}
        svn
        svnserve.conf 
	svnserve.tmpfiles
	svnserve.service
        subversion.rpath.fix.patch
        ruby-frozen-nil.patch)
sha256sums=('6ba8e218f9f97a83a799e58a3c6da1221d034b18d9d8cbbcb6ec52ab11722102'
            'SKIP'
            'ad2a082421c0c8b42ef5638ca3bfb9bcacac8bcab9b31ca519a108b02cecd760'
            '3b8e8d938daedc895804e136eef68e01072210e7686fdb98fb15cb8df24570dd'
            '6ac5c1d0e149178fbf478e0e8273e6350e1de95a197f13c7aa61b108a8f3b123'
            '9efc61a525494f8db70fb39447ffb55b3d5eb3b5bfdad186d96d999e155877ba'
            '138d8b1ff7adb4519c5790eb80c69b441abd9b5ad79d75a8fd7c226e6ba21deb'
            'e6846ca6b5136a3d217244dbebdcc8d8b449aaf61d09541e4f0aa76bee6c3845')
validpgpkeys=('19BBCAEF7B19B280A0E2175E62D48FAD16A0DE01'
              '8BC4DAE0C5A4D65F404401074F7DBAA99A59B973'
              'BA3C15B1337CF0FB222BD41A1BCA6586A347943F'
              'E7B2A7F4EC28BE9FF8B38BA4B64FFF1209F9FA74'
              '3D1DC66D6D2E0B9039528138C4A6C625CCC8E1DF'
              'A844790FB5743606EE95920776D788E1ED1A599C'
              '7B8CA7F6451AD89C8ADC077B376A3CFD110B1C95'
              '056F8016D9B87B1BDE41746799EC741B57921ACC'
              '3F8E467CB3366E3013E1120D583F00ADF981C39F'
              '91BFBF4D6956BD5DF7B72D23DFE691AE331BA3DB')
# Ben Reser / Stefan Sperling / Branko ÄŒibej / Evgeny Kotkov / Bert Huijben / Philip Martin /
# Stefan Fuhrmann / Stefan Hett (CODE SIGNING KEY) /Nathan Hartman (CODE SIGNING KEY) / James McCoy
prepare() {
   cd ${pkgname}-${pkgver}
   patch -Np0 -i ../subversion.rpath.fix.patch
   patch -p1 -i ../ruby-frozen-nil.patch
   rm subversion/bindings/swig/proxy/*.swg
   
   # The command line switch is broken
   sed -i 's|/include|/include/python3.6/py3c|' build/ac-macros/py3c.m4
   
   ./autogen.sh
}

build() {
   cd ${pkgname}-${pkgver}
   ./configure --prefix=/usr --with-apr=/usr --with-apr-util=/usr \
               --with-zlib=/usr --with-serf=/usr --with-apxs \
               --with-sqlite=/usr --with-jdk=/usr/lib/jvm/java-8-openjdk \
               --enable-javahl --without-gnome-keyring --with-kwallet \
               --with-apache-libexecdir=/usr/lib/httpd/modules \
               --with-py3c=/usr \
               --disable-static

   make LT_LDFLAGS="-L$Fdestdir/usr/lib"
   make swig_pydir=/usr/lib/python3.6/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python3.6/site-packages/svn swig-py swig-pl javahl swig-rb
}

package() {
   cd ${pkgname}-${pkgver}

   export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
   make DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
     swig_pydir=/usr/lib/python3.6/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python3.6/site-packages/svn \
     install install-swig-py install-swig-pl install-javahl install-swig-rb

   install -dm755 "${pkgdir}"/usr/share/subversion
   cp -a tools/hook-scripts "${pkgdir}"/usr/share/subversion/
   rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

   # xinetd
   install -D -m 644 "${srcdir}"/svn "${pkgdir}"/etc/xinetd.d/svn
   
   # svnserve
   install -D -m 644 "${srcdir}"/svnserve.conf "${pkgdir}"/etc/conf.d/svnserve

   # systemd
   install -D -m 644 "${srcdir}"/svnserve.service  "${pkgdir}"/usr/lib/systemd/system/svnserve.service
   install -D -m 644 "${srcdir}"/svnserve.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/svnserve.conf

   # bash completion
   install -Dm 644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
   for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
   done
}

