pkgname=bash
_basever=5.0
_patchlevel=011
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('x86_64')
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
groups=('base')
backup=(etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('readline>=7.0' glibc ncurses)
optdepends=('bash-completion: for tab completion')
provides=('sh')
install=bash.install
source=(https://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout)
sha1sums=('d116b469b9e6ea5264a74661d3a4c797da7f997b'
          'SKIP'
          '3211397399c367a501d103d36abfe5c7cadfeab2'
          '672c3c0c28c3642a5cff0e4093ff98359c747ff0'
          '9fd0cfda5b85651169f8761a834941b1f6f53952'
          '895ffd3f5c729271859a659c4ca17aefc3fda927'
          '0bc4dc72ef407eefe27b6ba5b384ccddb3e0852b'
          '9b8c7b32bf434d36b030c37b54fe8cebe54e2ab7'
          '44a3a849a6826571115f9920405edeca3d8287ad'
          'f371117fb06427e2b480fb796ee27f467a2d951a'
          'e0adfba3d5e9b96911c771140e03ec892e732be3'
          '08aac2d877b65e5816c3a73ee49a78261988597c'
          '9cf8246582e3a03f79791a6922c8331c2564a51e'
          '43e1f09bc57bb442603d4c8691d6f39e378a0bfc'
          '953e54638c2e0f3bed5f6589df6b9710f5d6bd35'
          '38e2101c03c9d48595df248a5115b61bd62c0b00'
          'f938ba0730f63f4535eaf4298ae4ebe1121553ca'
          'c8dfa690a081b15826737cab955ff2d79c69dd7a')
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
    for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
	source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p))
    done
fi

prepare() {
  cd $pkgname-$_basever

  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    msg "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
  done

}

build() {
  cd $pkgname-$_basever

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/bin:/usr/bin:/usr/local/sbin:/usr/sbin\"\'
	       -DSTANDARD_UTILS_PATH=\'\"/usr/bin:/usr/sbin\"\'
	       -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
	       -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\'
               -DNON_INTERACTIVE_LOGIN_SHELLS)
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure --prefix=/usr --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline
  make
}

check() {
  cd $pkgname-$_basever
  make check
}

package() {
  cd $pkgname-$_basever
  make DESTDIR=${pkgdir} install

  ln -s bash ${pkgdir}/usr/bin/sh

  install -dm755 ${pkgdir}/etc/skel/
  # system-wide configuration files
  install -m644 ${srcdir}/system.bashrc ${pkgdir}/etc/bash.bashrc
  install -m644 ${srcdir}/system.bash_logout ${pkgdir}/etc/bash.bash_logout
  
  # user configuration file skeletons
  install -m644 ${srcdir}/dot.bashrc ${pkgdir}/etc/skel/.bashrc
  install -m644 ${srcdir}/dot.bash_profile ${pkgdir}/etc/skel/.bash_profile
  install -m644 ${srcdir}/dot.bash_logout ${pkgdir}/etc/skel/.bash_logout
}
