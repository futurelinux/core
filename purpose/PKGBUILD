# Include global configuration

pkgname=purpose
pkgver=5.62.0
pkgrel=2
pkgdesc="Framework for providing abstractions to get the developer's purposes fulfilled"
arch=('x86_64')
url='https://projects.kde.org/purpose'
license=('LGPL')
depends=(kaccounts-integration kirigami2 accounts-qml-module hicolor-icon-theme)
makedepends=(extra-cmake-modules intltool python3)
optdepends=('kdeconnect: sharing to smartphone via KDE Connect')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('ed0bdc72b1b95fe988fb2ceba5cc1bd36b5bf00d30c098e9de50fdc36d3b3492'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
         -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_SYSCONFDIR=/etc \
        -DKDE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@" 
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
