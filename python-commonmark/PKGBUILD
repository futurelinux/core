# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-commonmark
pkgname=('python3-commonmark' 'python2-commonmark')
pkgver=0.9.0
pkgrel=1
pkgdesc='Python parser for the CommonMark Markdown spec'
url='https://pypi.python.org/pypi/CommonMark'
arch=('any')
license=('BSD')
makedepends=('python3-setuptools' 'python3-docutils' 'python3-future' 'python3-sphinx'
             'python2-setuptools' 'python2-docutils' 'python2-future' 'python2-sphinx')
checkdepends=('python3-hypothesis' 'python2-hypothesis')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rtfd/CommonMark-py/archive/${pkgver}.tar.gz)
sha256sums=('c30931518b2882cb03b90378167bc6d9b604ca1c0ff0d6227db6ab093ad9dfad')
sha512sums=('7a2121cf6cb7799940e8d6cb0fb73117e9d0c04ef500995a8213d1e77cfad840fbc0b43934f836a9f43c85c6b9acc2325d9ca01bf2281ef562319252dfdf8ef7')

prepare() {
  cp -r commonmark.py-${pkgver}{,-py2}
}

build() {
  (cd commonmark.py-${pkgver}
    python3 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd commonmark.py-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd commonmark.py-${pkgver}
    export PYTHONPATH=.
    python3 commonmark/tests/unit_tests.py
    python3 setup.py test
  )
  (cd commonmark.py-${pkgver}-py2
    export PYTHONPATH=.
    export PYTHONIOENCODING=UTF-8
    python2 commonmark/tests/unit_tests.py
    python2 setup.py test
  )
}

package_python3-commonmark() {
  depends=('python3-docutils' 'python3-future')

  cd commonmark.py-${pkgver}
  python3 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/commonmark-py.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  mv "${pkgdir}/usr/bin/cmark"{,-py3}
}

package_python2-commonmark() {
  depends=('python2-docutils' 'python2-future')

  cd commonmark.py-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/commonmark-py.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  mv "${pkgdir}/usr/bin/cmark"{,-py2}
}

# vim: ts=2 sw=2 et:
