# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-commonmark
pkgname=('python3-commonmark')
pkgver=0.9.1
pkgrel=1
pkgdesc='Python parser for the CommonMark Markdown spec'
url='https://pypi.python.org/pypi/CommonMark'
arch=('any')
license=('BSD')
makedepends=('python3-setuptools' 'python3-docutils' 'python3-future' 'python3-sphinx')
checkdepends=('python3-hypothesis')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rtfd/CommonMark-py/archive/${pkgver}.tar.gz)
sha256sums=('b5d5be6af35a183c3f915d714b31b61e6918894051114419f9ac0bbb715bf4c6')

build() {
  cd commonmark.py-${pkgver}
  python3 setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd commonmark.py-${pkgver}
   export PYTHONPATH=.
   python3 commonmark/tests/unit_tests.py
   python3 setup.py test
}

package_python3-commonmark() {
  depends=('python3-docutils' 'python3-future')

  cd commonmark.py-${pkgver}
  python3 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/commonmark-py.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
