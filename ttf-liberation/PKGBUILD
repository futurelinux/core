pkgname=ttf-liberation
pkgver=2.1.0
pkgrel=1
pkgdesc="Red Hats Liberation fonts"
arch=('any')
license=('custom:OFL')
url="https://github.com/liberationfonts/liberation-fonts"
makedepends=('fontforge' 'python3-fonttools')
provides=('ttf-font')
source=("$url/archive/$pkgver/liberation-fonts-$pkgver.tar.gz")
sha256sums=('a7761e42b43dfd0b4580f411829062ef98a6de6c2bafe0bfaf3041eb057d9107')

build() {
  cd liberation-fonts-$pkgver
  make
}

package() {
  cd liberation-fonts-$pkgver

  install -Dm644 -t "$pkgdir/usr/share/fonts/${pkgname#ttf-}" \
    liberation-fonts-ttf-$pkgver/*.ttf

  # install license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
