# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

pkgname=python3-sphinx-testing
pkgver=1.0.1
pkgrel=1
pkgdesc="Testing utility classes and functions for Sphinx extensions"
arch=('any')
url="https://github.com/sphinx-doc/sphinx-testing"
license=('BSD')
makedepends=('python3-setuptools')
depends=('python3-six' 'python3-sphinx')
checkdepends=('python3-mock' 'python3-sphinx')
source=("https://pypi.io/packages/source/s/sphinx-testing/sphinx-testing-$pkgver.tar.gz")
sha256sums=('ef661775b5722d7b00f67fc229104317d35637a4fb4434bf2c005afdf1da4d09')

build() {
  cd "$srcdir/sphinx-testing-$pkgver"
  python3 setup.py build
}

check() {
  export PYTHONPATH="$srcdir/sphinx-testing-$pkgver/src"
  cd "$srcdir/sphinx-testing-$pkgver/tests"
  python3 -m unittest discover
}

package() {
  cd "$srcdir/sphinx-testing-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
