pkgname=jack
pkgver=0.125.0
pkgrel=5
pkgdesc="A low-latency audio server"
arch=('x86_64')
license=('GPL2' 'custom:LGPL2')
depends=('readline' 'libsamplerate')
makedepends=('doxygen' 'libffado' 'celt')
optdepends=('libffado: FireWire support'
            'celt: NetJACK driver')
url="https://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
provides=("jack-audio-connection-kit=${pkgver}")
replaces=('jack-audio-connection-kit')
install=$pkgname.install
source=("$pkgname::git+https://github.com/jackaudio/${pkgname}1.git#tag=${pkgver}"
        "git+https://github.com/jackaudio/example-clients"
        "git+https://github.com/jackaudio/headers"
        "git+https://github.com/jackaudio/tools"
        40-hpet-permissions.rules
        99-audio.conf)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '23eba69d2dbbb800f68176e5d38b9af5'
         '4d928a76e3f3d77d037c42ab75ac0f0b')

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  git submodule init
  git config submodule.example-clients.url "${srcdir}/example-clients"
  git config submodule.jack.url "${srcdir}/headers"
  git config submodule.tools.url "${srcdir}/tools"
  git submodule update
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver
  
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/99-audio.conf" \
    "$pkgdir/etc/security/limits.d/99-audio.conf"

  install -Dm644 "$srcdir/40-hpet-permissions.rules" \
    "$srcdir/lib/udev/rules.d/40-hpet-permissions.rules"
}

