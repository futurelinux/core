# Maintainer:   totte <totte@chakralinux.org>
# Contributors: Andrea Scarpino <andrea@archlinux.org>
#               Antonio Rojas <arojas@archlinux.org>
#               Felix Yan <felixonmars@archlinux.org>
#               Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=(akonadi libakonadi)
pkgver=19.12.3
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
url="https://kontact.kde.org"
arch=(x86_64)
license=(LGPL)
makedepends=(extra-cmake-modules postgresql qt5-tools boost kitemmodels kaccounts-integration)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        akonadi-apparmor.patch::"https://cgit.kde.org/akonadi.git/patch/?id=9c33b1b4")
sha256sums=('e41714d81ecbb629aaa0b267e0c32a4b1d83c6a45cf3f37d52232003b4c0f325'
            'SKIP'
            '1808e7c110b588d6e9aec53f4b72bc41b3833ecb71309bb602026f6f45fa56bb')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgbase-$pkgver
  patch -p1 -i ../akonadi-apparmor.patch # Fix AppArmor profile
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON "$@"
  make
}

package_libakonadi() {
  pkgdesc='Libraries used by applications based on Akonadi'
  depends=('kitemmodels' 'kaccounts-integration' 'hicolor-icon-theme')

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/bin  # Provided by akonadi
}

package_akonadi() {
  depends=('libakonadi' 'mariadb')
  optdepends=('postgresql: PostgreSQL backend')

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}}  # Provided by libakonadi
}
