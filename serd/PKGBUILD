pkgname=serd 
pkgver=0.30.2
pkgrel=1
pkgdesc="A lightweight C library for RDF syntax which supports reading and writing Turtle and NTriples."
arch=('x86_64')
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('glibc')
makedepends=('python3')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('349c0b97b007751d802020b6088d6ae9')
 
build() {
  cd $pkgname-$pkgver
 
  # remove ldconfig
  sed -i "/ldconfig/d" wscript
 
  python3 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python3 ./waf
}
 
package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" python3 ./waf install
 
  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
