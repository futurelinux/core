pkgname=cracklib
pkgver=2.9.7
pkgrel=1
pkgdesc="Password Checking Library"
arch=('x86_64')
license=('GPL')
url="https://sourceforge.net/projects/cracklib"
depends=('glibc' 'zlib')
source=(https://github.com/cracklib/cracklib/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8b6fd202f3f1d8fa395d3b7a5d821227cfd8bb4a9a584a7ae30cf62cea6287dd')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --without-python
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  # symlink cracklib-small #34778
  mkdir -p $pkgdir/usr/share/dict
  ln -sf /usr/share/cracklib/cracklib-small $pkgdir/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small \
    | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}
