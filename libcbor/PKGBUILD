# Maintainer:   totte <totte@chakralinux.org>
# Contributors: https://git.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/libcbor

pkgname=libcbor
pkgver=0.6.1
pkgrel=2
pkgdesc='C library for parsing and generating CBOR, the general-purpose schema-less binary data format'
url='https://github.com/PJK/libcbor'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'cmocka')
provides=('libcbor.so')
source=(https://github.com/PJK/libcbor/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2f805f5fa2790c4fdd16046287f5814703229547da2b83009dd32357623aa182')

build() {
  mkdir -p ${pkgname}-${pkgver}/build
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -DWITH_TESTS=1
  make cbor cbor_shared VERBOSE=1
  make -C test VERBOSE=1
}

check() {
  cd ${pkgname}-${pkgver}/build
  make -C test test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
